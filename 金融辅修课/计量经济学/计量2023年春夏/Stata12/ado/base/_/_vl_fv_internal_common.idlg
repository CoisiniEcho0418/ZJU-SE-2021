/*

*!  VERSION 1.0.0  14may2009

*/

INCLUDE _std_msmall
POSITION . . _dlgwd 405

OK ok1, label("OK")
CANCEL can1, label("Cancel")
RESET res1
HELP hlp1, view("help varlist")
COPY copy1

SCRIPT PREINIT
BEGIN
	create STRING s_output
	create STRING s_local

	create BOOLEAN b_isAnova
	create BOOLEAN b_isMainEffect
	create BOOLEAN b_allowTS
	
	b_isAnova.setfalse
	b_isMainEffect.setfalse
	b_allowTS.setfalse
END

PROGRAM POSTINIT_PROGRAM
BEGIN
	if __MESSAGE {
		call main.vl_varlist.setvalue ""
		call __MESSAGE.withvalue main.vl_varlist.smartinsert "@"
	}
END

DIALOG main, tabtitle("Main") 	///
	label("Create varlist with factor variables")
BEGIN
  GROUPBOX	gb_type		_lft	_top	_iwd	_ht4,		///
	label("Type of variable")
  RADIO		rb_fv		_ilft	_ss	_ibwd	., first	///
	label("Factor variable") onclickon(program fv_on)
  RADIO		rb_var		@	_ss	@	., last 	///
	label("Simple variable") onclickon(program simple_on)


  GROUPBOX	gb_add		_lft	_xls	_iwd	80,		///
	label("")

  TEXT		tx_var		_ilft	_ms	_vnwd	.,		///
	label("Variable:")
  DEFINE holdy @y
  VARNAME	vl_var		@	_ss	@ 	.,		///
	label("Variable")
  BUTTON	bu_add		_irj80	@	80	.,		///
	label("Add to varlist") onpush(program add)
	
  INCLUDE _fv_core

  TEXT   	tx_varlist	_lft	355	_iwd	.,		///
	label("Varlist:")
  EDIT		vl_varlist	_lft	_ss	_iwd	.,
END

INCLUDE _fv_core_pr

PROGRAM simple_on
BEGIN
	call main.gb_add.setlabel "Add simple variable"
	call main.gb_add.setposition . . . 80
	call main.tx_var.show
	call main.vl_var.show
	call main.bu_add.setposition . . . .	

	call main.tx_term.hide
	call main.cb_term.hide

	call main.tx_variables.hide
	call main.tx_base.hide
 	call main.tx_cont.hide
	call main.tx_timeop.hide

	call program hide_terms
END

PROGRAM fv_on
BEGIN
	call main.gb_add.setlabel "Add factor variable"
	call main.gb_add.setposition . . . 255
	call main.tx_var.hide
	call main.vl_var.hide
	call main.bu_add.setposition . . . .

	call main.tx_term.show
	call main.cb_term.show

	call main.tx_variables.show
	call main.tx_base.show
 	call main.tx_cont.show
 	
 	if b_allowTS {
		call main.tx_timeop.show
	}
	else {
		call main.tx_timeop.hide
	}
	call program check_term
END

PROGRAM add
BEGIN
	require main.vl_var

	call s_output.setvalue ""

	if main.rb_var {
		call main.vl_var.withvalue main.vl_varlist.smartinsert "@"
	}
	if main.rb_fv {
		put /program _fv_core_add
	}

	call s_output.withvalue main.vl_varlist.smartinsert "@"
END

PROGRAM command
BEGIN
	put  main.vl_varlist
END

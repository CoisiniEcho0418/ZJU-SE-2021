/*
    ameans.dlg

*!  VERSION 1.0.2  28feb2005

*/

VERSION 8.0

INCLUDE _std_medium
INCLUDE header

HELP hlp1, view("help ameans")
RESET res1

DIALOG main, label("ameans - Arithmetic, geometric, and harmonic means") /*
		*/ tabtitle("Main")
BEGIN

	TEXT	 tx_var		_lft	_top	_iwd	.,		/*
		 */	label("Variables: (leave empty for all)")	
		
	VARLIST	 vl_var		@	_ss	@	.,		/*
		 */	label("Variables")				

	CHECKBOX ck_add		@	_ls	@	_ht9,		/*
		 */	label("Add value to variables")			/*
		 */	groupbox					/*
		 */	onclickon(script main_add_on) 			/*
		 */	onclickoff(script main_add_off)

	TEXT     tx_value       _ilft   _ss     _inwd   .,		/*
		 */	label("Value:")
	EDIT	 en_all		@       _ss	_en14wd	.,		/*
		 */	label("Value")					/*
		 */	numonly						/*
		 */     option(add)

	RADIO	 rb_all		@	_ms	_ibwd	.,		/*
		 */	label("Apply to all specified variables")	/*
		 */	first						/*
		 */     option(NONE)

	RADIO	 rb_only	@	_ss	@	.,		/*
		 */	label("Apply only to variables with nonpositive values") /*
		 */	last						/*
		 */     option(only)

	DEFINE _x _lft
	DEFINE _cx _spr
	DEFINE _y _xxls
	INCLUDE _sp_level 

END

SCRIPT main_add_on
BEGIN
	main.tx_value.enable
	main.en_all.enable
	main.rb_all.enable
	main.rb_only.enable
END

SCRIPT main_add_off
BEGIN
	main.tx_value.disable
	main.en_all.disable
	main.rb_all.disable
	main.rb_only.disable
END


INCLUDE byifin
INCLUDE weights_fa

PROGRAM command
BEGIN
	INCLUDE _by_pr
	put "ameans "
	varlist [main.vl_var]
	INCLUDE _ifin_pr
	INCLUDE _weights_pr
	beginoptions
		if main.ck_add {
			require  main.en_all
			optionarg main.en_all
		}
		option main.rb_only
		
		INCLUDE _level_main_pr 
	endoptions
END


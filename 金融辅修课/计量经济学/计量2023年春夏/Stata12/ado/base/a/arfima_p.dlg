/*
	predict (after arfima)

*!  VERSION 1.0.0  06may2011

*/

VERSION 12.0

INCLUDE _std_medium
INCLUDE header

HELP hlp1, view("help arfima postestimation")
RESET res1

SCRIPT POSTINIT
BEGIN
	script _p_gentype_setMedium
END

DIALOG main, label("predict - Prediction after estimation") tabtitle("Main")
BEGIN
  INCLUDE  _p_gentype

  GROUPBOX gb_opts		_lft	_ls	_iwd	_ht8,		///
	label("Produce:")
  RADIO rb_xb			_indent	_ss	_inwd	., first	///
	option(xb)							///
	onclickon("script main_xb_on")					///
	onclickoff("script main_xb_off")				///
	label("Predicted values")
  RADIO rb_residuals		@	_ss	@	.,		///
	option(residuals)						///
	onclickon("script main_res_on")					///
	onclickoff("script main_res_off")				///
	label("Predicted innovations")
  RADIO rb_rstandard		@	_ss	@	.,		///
	option(rstandard)						///
	label("Standardized innovations")
  RADIO rb_fdifference		@	_ss	@	.,last		///
	option(fdifference)						///
	label("Fractional difference")
END

SCRIPT main_xb_on
BEGIN
	opts.tx_rmse.enable
	opts.ed_rmse.enable
	opts.tx_dynamic.enable
	opts.ed_dynamic.enable
END

SCRIPT main_xb_off
BEGIN
	opts.tx_rmse.disable
	opts.ed_rmse.disable
	opts.tx_dynamic.disable
	opts.ed_dynamic.disable
END

SCRIPT main_res_on
BEGIN
	opts.tx_rmse.enable
	opts.ed_rmse.enable
END

SCRIPT main_res_off
BEGIN
	opts.tx_rmse.disable
	opts.ed_rmse.disable
END

INCLUDE _p_gentype_sc
INCLUDE ifin
INCLUDE _type_list_fd

DIALOG opts, tabtitle("Options")
BEGIN
  TEXT tx_rmse			_lft	_top	_iwd	.,		///
	label("Store root mean squared errors of predicted statistics in new variables:")
  EDIT ed_rmse			@	_ss	@	.,		///
	option(rmse)							///
	label("Store root mean squared errors of predicted statistics in new variables")

  TEXT tx_dynamic		@	_ls	_vnwd	.,		///
	label("Dynamic forecasts:")
  EDIT ed_dynamic		@	_ss	_cwd1	.,		///
	option(dynamic)							///
	label("Dynamic forecasts")
END

PROGRAM command
BEGIN
	put "predict "
	put " " /program _p_gentype_output
	put " " /program ifin_output
	beginoptions
	       option radio(main rb_xb rb_residuals rb_rstandard rb_fdifference)
		if opts.ed_rmse.isenabledd() {
			optionarg opts.ed_rmse
		}
		if opts.ed_dynamic.isenabled() {
			optionarg opts.ed_dynamic
		}
	endoptions
END

/*
   cci.dlg

*! VERSION 1.2.0  23may2007

*/

VERSION 10.0

INCLUDE _std_medium
INCLUDE _ht280
INCLUDE header

HELP hlp1, view("help cci")
RESET res1

DEFINE     rowhght    31
DEFINE     rowhght2   61
DEFINE     rowskip    +5
DEFINE     rowskip2   +25

DEFINE     col1wd     75
DEFINE     col2       100
DEFINE     col2wd     120
DEFINE     col2i      110
DEFINE     col3       230
DEFINE     col3i      229
DEFINE 	   spwd       100

PROGRAM POSTINIT_PROGRAM
BEGIN
	if __MESSAGE.iseq("-exact-") {
		call main.ck_exact.seton
	}
END

DIALOG main, label("cci - Case-control studies") tabtitle("Main")
BEGIN
  TEXT     tx_exp1	col2i	_top	spwd	.,		///
	center label("Exposed")
  TEXT     tx_unexp1	col3i	@	spwd	.,		///
	center label("Unexposed")
  FRAME    fr_exp2	_lft	_ms	_iwd	rowhght,
  FRAME    fr_exp1	col2	@	col2wd	rowhght2,

  TEXT     tx_goto_e	_lft	@	0	_ht0,
  TEXT     tx_cases1	_indent	rowskip	col1wd	., label("Cases")
  SPINNER  sp_a		col2i	@	spwd	., 		///
	label("a") min(0) max(10000000)
  SPINNER  sp_b		col3i	@	spwd	.,		///
	label("b") min(0) max(10000000)

  FRAME    fr_unexp2	_lft   rowskip2	_iwd	rowhght,

  TEXT     tx_controls	_indent	rowskip	col1wd	.,		///
	label("Controls")
  SPINNER  sp_c		col2i	@	spwd	.,		///
	label("c") min(0) max(10000000)
  SPINNER  sp_d		col3i	@	spwd	.,		///
	label("d") min(0) max(10000000)

  GROUPBOX gb_box	_lft	_xls	_iwd	_ht8,
  RADIO    rb_exact	_ilft	_ss	_inwd	.,		///
	option(NONE) first label("Exact confidence intervals")
  RADIO    rb_cornfield	@	_ss	@	.,		///
	option(cornfield) label("Cornfield approximation")
  RADIO    rb_woolf	@	_ss	@	.,		///
	option(woolf) label("Woolf approximation")
  RADIO    rb_tb	@	_ss	@	.,		///
	option(tb) label("Test-based confidence intervals")	///
	last

  DEFINE _x _lft2
  DEFINE _y +45
  DEFINE _cx _spr2b
  INCLUDE _sp_level

  CHECKBOX ck_exact	_lft	@	_cwd1	.,		///
	option(exact) label("Fisher's exact p")
END

PROGRAM command
BEGIN
	put "cci "
	put main.sp_a " " main.sp_b " " main.sp_c " " main.sp_d
	beginoptions
		option radio(main rb_exact rb_tb rb_cornfield rb_woolf)
		option main.ck_exact
		INCLUDE _level_main_pr
	endoptions
END

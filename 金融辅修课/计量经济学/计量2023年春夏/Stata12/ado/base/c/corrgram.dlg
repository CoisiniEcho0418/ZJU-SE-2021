/*
  corrgram

  VERSION 1.1.0  07oct2004

*/

VERSION 9.0

INCLUDE _std_medium
INCLUDE _ht230
INCLUDE header
HELP hlp1, view("help corrgram")
RESET res1

DIALOG main, label("corrgram - Table of autocorrelations") /*
		*/ tabtitle("Main")
BEGIN
  DEFINE _x _xsetbu
  DEFINE _y _top
  INCLUDE _bu_tsset
  TEXT     tx_var          _lft      _top      _cwd2     .,		/*
		*/label("Variable:")
  VARNAME  vn_var          @         _ss       _vnwd     .,             /*
  		*/label("Variable")
  GROUPBOX gb_opt          @         +35       _iwd      _ht12,         /*
  		*/label("Options")
  RADIO    rb_lags1        _indent   _ss       _ibwd     .,             /*
  		*/label("Use default number of autocorrelations -- min([n/2]-2,40)")/*
  		*/first							/*
		*/clickon(gaction main.sp_lags2.disable)
  RADIO    rb_lags2        @         _ss       @         ., 	        /*
  		*/clickon(gaction main.sp_lags2.enable)	 		/*
  		*/label("Use specified number of autocorrelations")	/*
		*/last
  DEFINE xpos @x
  SPINNER  sp_lags2        _indent2  _ss       _spwd     .,           	/*
  		*/min(1)						/*
		*/max(100)						/*
		*/default(1)						/*
		*/option("lags")
  CHECKBOX ck_noplot       xpos      _ls       _ibwd     .,		/*
  		*/ label("Suppress character-based plots")		/*
  		*/ option("noplot")
  CHECKBOX ck_yw           @         _ms       @         .,		/*
  		*/ option(yw)						/*
  		*/ label("Calculate partial autocorrelations using Yule-Walker equations")
END

INCLUDE ifin

PROGRAM command
BEGIN
	put "corrgram "
	varlist main.vn_var
	INCLUDE _ifin_pr
	beginoptions
		if main.rb_lags2 {
			optionarg main.sp_lags2
		}
		option main.ck_noplot
		option main.ck_yw
	endoptions
END


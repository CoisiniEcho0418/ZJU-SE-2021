/*
	cusum_9.dlg

*!  VERSION 1.1.1  09aug2006  (deprecated)

*/

VERSION 9.0

INCLUDE graph_header
INCLUDE header

HELP hlp1, view("help cusum")
RESET res1

DIALOG main, label("cusum - Cusum plots and tests for binary variables") tabtitle("Main")
BEGIN
  TEXT     tx_xvar       _lft      _top    _vnwd      .,	///
	label("X variable:")
  VARNAME  vn_xvar           @      _ss    _vnwd      .,	///
	label("X variable")

  TEXT     tx_yvar           +150   _top   _vnwd      .,	///
	label("Y variable:")
  VARNAME  vn_yvar           @      _ss    _vnwd      ., 	///
	label("Y variable")

  CHECKBOX ck_gen         _lft      _ls     _cwd1     .,	///
	label("Generate variable for cusum:")			///
	onclickon(main.ed_gen.enable)				///
	onclickoff(main.ed_gen.disable)
  EDIT     ed_gen     _indent2      _ss    _vnwd      .,	///
	option(generate)					///
	label("Generate variable for cusum")
  CHECKBOX ck_yfit        _lft      _ls     _cwd1     .,	///
	label("Calculate cusum against variable:")		///
	onclickon(main.vn_yfit.enable)				///
	onclickoff(main.vn_yfit.disable)
  VARNAME  vn_yfit    _indent2      _ss    _vnwd      .,	///
	option(yfit) label("Calculate cusum against variable")
  CHECKBOX ck_nograph     _lft      _ls     _cwd1     .,	///
	option(nograph) label("Suppress the plot")		///
	onclickoff(script graph_on) onclickon(script graph_off)
  CHECKBOX ck_nocalc      _lft      _ms     _cwd1     .,	///
	option(nocalc) label("Suppress cusum test statistics")
END

SCRIPT graph_on
BEGIN
	/* turn on all the graphs tabs */
	script p1_opts_enable
	script gr_twoway_enable
	script addplot_enable
END

SCRIPT graph_off
BEGIN
	/* turn off all the graphs tabs */
	script p1_opts_disable
	script gr_twoway_disable
	script addplot_disable
END

INCLUDE ifin
INCLUDE p1_opts
INCLUDE addplot
INCLUDE gr_twoway

SCRIPT PREINIT
BEGIN
	script  p1_opts_createAsNoRange
	script p1_opts_setDefaultLine
END

SCRIPT POSTINIT
BEGIN
	p1_opts.settabtitle "Cusum plot"
	script addplot_init
END

PROGRAM command
BEGIN
	put "cusum "
	require main.vn_xvar
	varlist main.vn_yvar
	varlist main.vn_xvar
	INCLUDE _ifin_pr
	beginoptions
		if main.ck_yfit {
			optionarg main.vn_yfit
		}
		option main.ck_nograph
		option main.ck_nocalc
		if main.ck_gen {
			optionarg main.ed_gen
		}

		put " " /program p1_opts_recast_output
		put " " /program addplot_output
		put " " /program gr_twoway_output
	endoptions
END
/* end */

/*
  dfactor

*!  VERSION 1.1.0  23aug2010

  keyword:  eclass

*/

VERSION 12.0

INCLUDE _std_large
DEFINE _dlght 550
INCLUDE header

HELP hlp1, view("help dfactor")
RESET res1

SCRIPT PREINIT
BEGIN
	script se_createAsRobustOIM
END

SCRIPT POSTINIT
BEGIN
	program se_setFinalInitState
	script max_nonrtolerance_hide
END

DIALOG main, label("dfactor - Dynamic-factor model") tabtitle("Model")
BEGIN
  DEFINE _x _xsetbu
  DEFINE _y _top
  INCLUDE _bu_tsset
  GROUPBOX gb_obs_eqs	_lft	_ss	_iwd	_ht19h,			///
	label("Observation equations")
  TEXT tx_obs_dep	_indent	_ss	_inwd	.,			///
	label("Dependent variables:")
  VARLIST vl_obs_dep	@	_ss	@	.,			///
	allowcat							///
	ts 								///
	label("Dependent variables")
  TEXT tx_obs_exog	@	_ls	@	.,			///
	label("Exogenous variables:")
  VARLIST vl_obs_exog	@	_ss	@	.,			///
	allowcat							///
	ts fv 								///
	label("Exogenous variables")
  TEXT tx_obs_ars	@	_ls	_cwd1	.,			///
	label("AR structure:")
  DEFINE holdy @y
  COMBOBOX cb_obs_ars	@	_ss	@	.,			///
	dropdownlist							///
	contents(arstructure_list)					///
	values(arstructure_values)					///
	option(arstructure)						///
	label("Structure of AR coefficient matrix")
  TEXT tx_obs_ar	_lft2	holdy	_cwd2	.,			///
	label(`"List of AR lags: (e.g., "1/3")"')
  EDIT ed_obs_ar	@	_ss	_cwd6	.,			///
	option(ar)							///
	label("List of AR lags")
  TEXT tx_obs_covs	_ilft	_ls	_cwd1	.,			///
	label("Covariance structure:")
  COMBOBOX cb_obs_covs	@	_ss	@	.,			///
	dropdownlist							///
	contents(ocovstructure_list)					///
	values(ocovstructure_values)					///
	option(covstructure)						///
	label("Covariance structure of the disturbances")
  CHECKBOX ck_obs_nocon	_lft2	@	_cwd1	.,			///
	option("noconstant")						///
	label("Suppress constant term")

  CHECKBOX ck_fac_eqs	_lft	+45	_iwd	_ht19h,			///
	groupbox							///
	onclickon(script main_ck_fac_eqs_on)				///
	onclickoff(script main_ck_fac_eqs_off)				///
	label("Use a factor equation")
  TEXT tx_fac_eqs	_indent	_ss	_inwd	.,			///
	label("Unobserved factors:")
  EDIT ed_fac_eqs	@	_ss	@	.,			///
	label("Unobserved factors")
  TEXT tx_fac_exog	@	_ls	@	.,			///
	label("Exogenous variables that enter the equation for factors:")
  VARLIST vl_fac_exog	@	_ss	@	.,			///
	allowcat							///
	ts fv								///
	label("Exogenous variables that enter the equation for factors:")
  TEXT tx_fac_ars	@	_ls	_cwd1	.,			///
	label("AR structure:")
  DEFINE holdy @y
  COMBOBOX cb_fac_ars	@	_ss	@	.,			///
	dropdownlist							///
	contents(arstructure_list)					///
	values(arstructure_values)					///
	option(arstructure)						///
	label("Structure of AR coefficient matrix")
  TEXT tx_fac_ar	_lft2	holdy	_cwd2	.,			///
	label(`"List of AR lags: (e.g., "1/3")"')
  EDIT ed_fac_ar	@	_ss	_cwd6	.,			///
	option(ar)							///
	label("List of AR lags")
  TEXT tx_fac_covs	_ilft	_ls	_cwd1	.,			///
	label("Covariance structure:")
  COMBOBOX cb_fac_covs	@	_ss	@	.,			///
	dropdownlist							///
	contents(covstructure_list)					///
	values(covstructure_values)					///
	option(covstructure)						///
	label("Covariance structure of the disturbances")

  TEXT     tx_constr 	_lft	_xls	_lw80	.,			///
	label("Constraints: (optional)")
  COMBOBOX cb_constr 	@	_ss	@	.,			///
	append								///
	dropdown							///
	contents(constraint)						///
	label("Constraints")						///
	option(constraints)
  BUTTON   bu_constr	_slw80	@	80	.,			///
	label("Manage...")						///
	onpush(script show_constraint_dialog)
END

LIST arstructure_list
BEGIN
	Diagonal matrix
	Lower triangular matrix
	General matrix
END

LIST arstructure_values
BEGIN
	""
	ltriangular
	general
END

LIST ocovstructure_list
BEGIN
	Diagonal matrix
	Identity matrix
	Diagonal scalar matrix
	Symmetric, positive-definite matrix
END

LIST ocovstructure_values
BEGIN
	""
	identity
	dscalar
	unstructured
END

LIST covstructure_list
BEGIN
	Identity matrix
	Diagonal scalar matrix
	Diagonal matrix
	Symmetric, positive-definite matrix
END

LIST covstructure_values
BEGIN
	""
	dscalar
	diagonal
	unstructured
END

SCRIPT main_ck_fac_eqs_on
BEGIN
	main.tx_fac_eqs.enable
	main.ed_fac_eqs.enable
	main.tx_fac_exog.enable
	main.vl_fac_exog.enable
	main.tx_fac_ars.enable
	main.cb_fac_ars.enable
	main.tx_fac_ar.enable
	main.ed_fac_ar.enable
	main.tx_fac_covs.enable
	main.cb_fac_covs.enable
END

SCRIPT main_ck_fac_eqs_off
BEGIN
	main.tx_fac_eqs.disable
	main.ed_fac_eqs.disable
	main.tx_fac_exog.disable
	main.vl_fac_exog.disable
	main.tx_fac_ars.disable
	main.cb_fac_ars.disable
	main.tx_fac_ar.disable
	main.ed_fac_ar.disable
	main.tx_fac_covs.disable
	main.cb_fac_covs.disable
END

INCLUDE _constraints_sc
INCLUDE byifin
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
END

DIALOG rpt, tabtitle("Reporting")
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr
  DEFINE _y _top
  INCLUDE _sp_level

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _nocnsreport

  DEFINE _x _lft
  DEFINE _y _ms
  DEFINE _cx _iwd
  INCLUDE _noomitted

  DEFINE _x _lft
  DEFINE _y _ms
  DEFINE _cx _iwd
  INCLUDE _vsquish

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _factor_vars_reporting

  DEFINE _x _lft
  DEFINE _y _xxxls
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr

INCLUDE max_ml

DIALOG adv, tabtitle("Advanced")
BEGIN
  TEXT tx_method	_lft	_top	360	.,			///
		label("Method for calculating log likelihood:")
  DEFINE holdy @y
  COMBOBOX cb_method	@	_ss	@	.,			///
	dropdownlist							///
	contents(method_list)						///
	values(method_values)						///
	option(method)							///
	label("Method for calculating log likelihood:")
END

LIST method_list
BEGIN
	Stationary Kalman filter and diffuse De Jong Kalman filter
	Stationary and diffuse De Jong Kalman filters
END

LIST method_values
BEGIN
	""
	dejong
END

PROGRAM obs_output
BEGIN
	varlist main.vl_obs_dep
	put " ="
	varlist [main.vl_obs_exog]
	beginoptions
		optionarg main.cb_obs_ars
		if (main.ed_obs_ar & !main.ed_obs_ar.isnumlist()) {
			stopbox stop `"There is an invalid numlist in "List of AR lags" for "Observation equations""'
		}
		optionarg main.ed_obs_ar
		optionarg main.cb_obs_covs
		option main.ck_obs_nocon
	endoptions
END

PROGRAM fac_output
BEGIN
	put main.ed_fac_eqs
	put " ="
	varlist [main.vl_fac_exog]
	beginoptions
		optionarg main.cb_fac_ars
		if (main.ed_fac_ar & !main.ed_fac_ar.isnumlist()) {
			stopbox stop `"There is an invalid numlist in "List of AR lags" for "Use a factor equations""'
		}
		optionarg main.ed_fac_ar
		optionarg main.cb_fac_covs
	endoptions
END

PROGRAM command
BEGIN
	INCLUDE _by_pr
	put "dfactor "
	put " ("
	put /program obs_output
	put ")"
	if main.ck_fac_eqs {
		require main.ed_fac_eqs
		put " ("
		put /program fac_output
		put ")"
	}

	INCLUDE _ifin_pr
	beginoptions
		INCLUDE _constraints_main_pr
		put " " /program se_output
		put " " /program max_output
		optionarg /hidedefault rpt.sp_level
		INCLUDE _nocnsreport_pr
		INCLUDE _noomitted_pr
		INCLUDE _vsquish_pr
		INCLUDE _factor_vars_reporting_pr
		put " " rpt_bu_fmtcoefResults
		optionarg adv.cb_method
	endoptions
END

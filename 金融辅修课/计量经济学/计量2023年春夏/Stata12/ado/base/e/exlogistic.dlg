/*
  exlogistic.dlg

*!  VERSION 1.1.7  14oct2010

*/

VERSION 10.0

INCLUDE _std_large
DEFINE _dlght 400
INCLUDE header

HELP hlp1, view("help exlogistic")
RESET res1

SCRIPT PREINIT
BEGIN
	script se_createAsInternalML
	script weights_setLayout_f
END

DIALOG main, label("exlogistic - Exact logistic regression")	///
	tabtitle("Model")
BEGIN
  INCLUDE _dviv
  
  TEXT     tx_condv	@	_ls	@	.,		///
	label("Independent variables: (do not report coefficients)")
  VARLIST  vl_condv	@	_ss	@	.,		///
	option(condvars)					///
	label("Independent variables (do not report coefficients)")

  CHECKBOX ck_group	_lft	_ls	_iwd	_ht4,		///
	groupbox						///
	onclickon(script group_on)				///
	onclickoff(script group_off)				///
	label("Data are stratified by groups with their own intercepts")
  TEXT     tx_group	_ilft	_ss	_ibwd	.,		///
	label("Group variable: (condition on intercepts for each value of the variable)")
  VARNAME  vn_group	@	_ss	_vnwd	.,		///
	option(group)						///
	label("Group variable (condition on intercepts for each value of the variable)") 

  CHECKBOX ck_binomial	_lft	_xls	_iwd	_ht6h,		///
	groupbox						///
	label("Data are in binomial form")			///
	onclickon(program binomial_on)				///
	onclickoff(script binomial_off)
  RADIO    rb_fixed	_ilft	_ss	_ibwd	.,		///
	first							///
	onclickon(program show_fixed_binomial)			///
	label("Fixed number of trials per observation")
  RADIO    rb_variable	@	_ss	@	.,		///
	last							///
	onclickon(program show_variable_binomial)		///
	label("Variable number of trials per observation")
  SPINNER  sp_binomial	+20	_ms	_vnwd	.,		///
	option(binomial) min(1) max(10000)
  VARNAME  vn_binomial	@	@	@	.,		///
	option(binomial)					///
	label("Trials per observation (variable)") 
  TEXT     tx_binomial	_vnsep	@	300	.,


  GROUPBOX gb_cons	_lft	_xls	_iwd	_ht6,		///
	label("Options for the constant")
  RADIO	   rb_default	_ilft	_ss	_ibwd	.,		///
	first							///
	label("Condition out the constants for each group, or the overall")
  RADIO    rb_nocons	@	_ss	@	.,		///
	label("Do not estimate or condition out the constant")	///
	option(nocons)
  RADIO	   rb_estcons	@	_ss	@	.,		///
	last							///
	option(estcons)						///
	label("Estimate the overall constant") 
END

SCRIPT group_on
BEGIN
	main.tx_group.enable
	main.vn_group.enable
	main.gb_cons.disable
	main.rb_default.disable
	main.rb_nocons.disable
	main.rb_estcons.disable
END

SCRIPT group_off
BEGIN
	main.tx_group.disable
	main.vn_group.disable
	main.gb_cons.enable
	main.rb_default.enable
	main.rb_nocons.enable
	main.rb_estcons.enable
END


PROGRAM show_fixed_binomial
BEGIN
	if main.rb_fixed {
		call main.sp_binomial.show
		call main.vn_binomial.hide
		call main.tx_binomial.setlabel "Trials per observation"
	}
END
PROGRAM show_variable_binomial
BEGIN
	if main.rb_variable {
		call main.sp_binomial.hide
		call main.vn_binomial.show
		call main.tx_binomial.setlabel "Trials per observation (variable)"
	}
END

PROGRAM binomial_on
BEGIN
	call main.rb_fixed.enable
	call main.rb_variable.enable
	call main.tx_binomial.enable
	call main.sp_binomial.enable
	call main.vn_binomial.enable
END

SCRIPT binomial_off
BEGIN
	main.rb_fixed.disable
	main.rb_variable.disable
	main.sp_binomial.disable
	main.vn_binomial.disable
	main.tx_binomial.disable
END

DIALOG terms, tabtitle("Terms")
BEGIN
  TEXTBOX  tb_note1	_lft	_top	_iwd	_tb2,		///
	label(Joint hypothesis tests of multiple coefficients:  ///
		tests that coefficients of all listed variables ///
		are jointly 0.)

  TEXT     tx_test	_lft	_xls	80	.,		///
	label("Joint test")

  TEXT     tx_vars	+85	@	_lw80	.,		///
	label("Variables to test")

  CHECKBOX ck_term1	_lft	_ms	80	., 		///
  	onclickon(terms.vn_term1.enable)			///
  	onclickoff(terms.vn_term1.disable)			///
  	label("Term 1")
  VARLIST  vn_term1	+85	@	_lw80	., 		///
	label("Term 1")
  
  CHECKBOX ck_term2	_lft	_ms	80	., 		///
  	onclickon(terms.vn_term2.enable)			///
  	onclickoff(terms.vn_term2.disable)			///
  	label("Term 2")
  VARLIST  vn_term2	+85	@	_lw80	., 		///
	label("Term 2")

  CHECKBOX ck_term3	_lft	_ms	80	., 		///
  	onclickon(terms.vn_term3.enable)			///
  	onclickoff(terms.vn_term3.disable)			///
  	label("Term 3")
  VARLIST  vn_term3	+85	@	_lw80	., 		///
	label("Term 3")

  CHECKBOX ck_term4	_lft	_ms	80	., 		///
  	onclickon(terms.vn_term4.enable)			///
  	onclickoff(terms.vn_term4.disable)			///
  	label("Term 4")
  VARLIST  vn_term4	+85	@	_lw80	., 		///
	label("Term 4")

  CHECKBOX ck_term5	_lft	_ms	80	., 		///
  	onclickon(terms.vn_term5.enable)			///
  	onclickoff(terms.vn_term5.disable)			///
  	label("Term 5")
  VARLIST  vn_term5	+85	@	_lw80	., 		///
	label("Term 5")
  
  CHECKBOX ck_term6	_lft	_ms	80	., 		///
  	onclickon(terms.vn_term6.enable)			///
  	onclickoff(terms.vn_term6.disable)			///
  	label("Term 6")
  VARLIST  vn_term6	+85	@	_lw80	., 		///
	label("Term 6")
END

INCLUDE byifin
INCLUDE weights

DIALOG opt, tabtitle("Options")
BEGIN
  SPINNER  sp_memory	_lft	_top	_spwd	.,		///
	default(10) min(1) max(2048)  
  TEXT     tx_mb	_spsep	@	250 	.,		///
  	label("Limit for memory usage in megabytes")
 
  CHECKBOX ck_save	_lft	+35	_iwd	_ht4h,		///
	label("Save the joint conditional distribution")	///
	groupbox						///
	onclickon(script opt_save_on)				///
	onclickoff(script opt_save_off)

  TEXT     tx_sav	_indent	_ss	_ibwd	.,		///
	label("Filename:")
  FILE     fi_sav	@	_ss	@	.,		///
	label("Browse...")					///
	option(saving)						///
	filter("Stata Dataset (*.dta)|*.dta|All (*.*)|*.*")	///
	defext(dta)						///
	save							///
	error("Filename")   		
END

DIALOG rpt, tabtitle("Reporting")
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr
  DEFINE _y _top
  INCLUDE _sp_level

  GROUPBOX gb_coefopt	_lft	_ls	_iwd	_ht4,		///
  	label("Coefficient options")

  RADIO    rb_or	_indent	_ss	_ibwd	., 		///
	first							///
	label("Report odds ratios (default)") 			///
	option(or)
  RADIO    rb_coef	@	_ss	@	.,		///
	last							///
	label("Report coefficients")				///
	option(coef)

  GROUPBOX gb_testopt	_lft	_xls	_iwd	_ht6,		///
  	label("Test options")
  
  RADIO    rb_suff	_indent	_ss	_ibwd	.,		///
	first							///
	option(test(suffidient))				///
	label("Report probability of observed sufficient statistic (default)")
  RADIO    rb_score	@	_ss	@	., 		///
	label("Report conditional scores test")			///
	option(test(score))
  RADIO    rb_prob	@	_ss	@ 	.,		///
	last 							///
	label("Report conditional probabilities test")		///
	option(test(probability))

  TEXT     tx_muev	_lft	+45	_iwd	.,		///
label("Report median unbiased estimates (MUEs) rather than CMLEs for:")
  VARLIST  vl_muev	@	_ss	@	.,		///
label("Report median unbiased estimates (MUEs) rather than CMLEs for") 	///
	option(mue)

  CHECKBOX ck_midp	_lft	_ls	_iwd	.,		///
	label("Use mid-p-value rule")				///
	option(midp)

  CHECKBOX ck_nolog	_lft	_ms	_iwd	.,		///
	label("Do not display enumeration log")			///
	option(nolog)
END

SCRIPT opt_save_on
BEGIN
	opt.fi_sav.enable
	opt.tx_sav.enable
END

SCRIPT opt_save_off
BEGIN
	opt.fi_sav.disable
	opt.tx_sav.disable
END

SCRIPT opt_suff_on
BEGIN
	opt.rb_suff.enable
	opt.rb_score.disable
	opt.rb_prob.disable
END

SCRIPT opt_score_on
BEGIN
	opt.rb_score.enable
	opt.rb_suff.disable
	opt.rb_prob.disable
END

SCRIPT opt_prob_on
BEGIN
	opt.rb_prob.enable
	opt.rb_suff.disable
	opt.rb_score.disable
END

PROGRAM terms_output_wrk
BEGIN
	call create BOOLEAN comma
	call comma.setfalse
	if terms.ck_term1 {
		varlist terms.vn_term1
		call comma.settrue
	}
	if terms.ck_term2 {
		if comma {
			put ", "
		}
		varlist terms.vn_term2
		call comma.settrue
	}
	if terms.ck_term3 {
		if comma {
			put ", "
		}
		varlist terms.vn_term3
		call comma.settrue
	}
	if terms.ck_term4 {
		if comma {
			put ", "
		}
		varlist terms.vn_term4
		call comma.settrue
	}
	if terms.ck_term5 {
		if comma {
			put ", "
		}
		varlist terms.vn_term5
		call comma.settrue
	}
	if terms.ck_term6 {
		if comma {
			put ", "
		}
		varlist terms.vn_term6
		call comma.settrue
	}
END

PROGRAM terms_output
BEGIN
	if terms.ck_term1 | terms.ck_term2		///
		| terms.ck_term3 | terms.ck_term4	///
		| terms.ck_term5 | terms.ck_term6	///
	{
		put " terms("
		put /program terms_output_wrk
		put ") "
	}
END

PROGRAM command
BEGIN
	allowxi
	put /program by_output " "
	put "exlogistic "
	varlist main.vn_dv main.vl_iv
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		optionarg main.vl_condv
		if main.ck_group {
			require main.vn_group
			optionarg main.vn_group
		}
		
		if main.ck_binomial  {
			if main.rb_variable {
				require main.vn_binomial
				optionarg main.vn_binomial
			}
			else {
				optionarg main.sp_binomial
			}
		}

		option main.rb_nocons
		option main.rb_estcons
		
		put " " /program terms_output
		
		if !opt.sp_memory.isdefault() {
			put "memory("
			put opt.sp_memory
			put "M)"
		}
		if opt.ck_save {
			require opt.fi_sav
			repfile opt.fi_sav
			put `"saving("'
			put opt.fi_sav
			put `", "'
			put "replace)"
		}

		optionarg /hidedefault rpt.sp_level
		option rpt.rb_coef
		option rpt.rb_score
		option rpt.rb_prob
		optionarg rpt.vl_muev
		option rpt.ck_midp
		option rpt.ck_nolog
	endoptions
END

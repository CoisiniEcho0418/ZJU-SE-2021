/*
	predict (after factor)

*!  VERSION 1.1.0  30may2007

*/

VERSION 10.0

INCLUDE _std_large
INCLUDE header

HELP hlp1, view("help factor postestimation")
RESET res1

DIALOG main, label("predict - Prediction after estimation") tabtitle("Main")
BEGIN
  INCLUDE  _p_gentype

  GROUPBOX gb_produce		_lft	_ls	_iwd	_ht4,		///
	label("Produce:")
  RADIO    rb_regress		_indent	_ss	_ibwd	.,	first	///
	option(regression) 						///
	onclickon(script _p_gentype_multiple)				///
	label("Factors scored by the regression scoring method")
  RADIO    rb_bartlett		@	_ss	@	.,	last	///
	option(bartlett) 						///
	onclickon(script _p_gentype_multiple)				///
	label("Factors scored by the Bartlett scoring method")

  GROUPBOX gb_opts		_lft	_xls	_iwd	_ht9h,		///
	label("Options")
  CHECKBOX ck_norotated		_ilft	_ss	_ibwd	.,		///
	option(norotated) label("Use unrotated results, even when rotated results are available")
  CHECKBOX ck_notable		@	_ms	@	.,		///
	option(notable) label("Suppress table of scoring coefficients")

  CHECKBOX ck_format    	@	_ms	@	.,		///
	onclickon(script format_on)					///
	onclickoff(script format_off)					///
	label("Overide display format for displaying the scoring coefficients")
  EDIT     en_format		+20	_ss	200	.,		///
	option(format)							///
	label("Display format")
  BUTTON   bu_format		+205	@	80	.,		///
	label("Create...")						///
	onpush(script showFormatDialog)					///
	tooltip("Create display format")
END

SCRIPT showFormatDialog
BEGIN
	create STRING formatString
	create CHILD format_chooser
	format_chooser.setExitString formatString
	format_chooser.setExitAction "main.en_format.setvalue class formatString.value"
END

SCRIPT format_on
BEGIN
  	main.en_format.enable
  	main.bu_format.enable
END

SCRIPT format_off
BEGIN
  	main.en_format.disable
	main.bu_format.disable
END

INCLUDE _p_gentype_sc
INCLUDE ifin
INCLUDE _type_list_fd

PROGRAM command
BEGIN
	put "predict "
	put " " /program _p_gentype_output
	INCLUDE _ifin_pr
	beginoptions
		option main.rb_regress
		option main.rb_bartlett
		option main.ck_norotated
		option main.ck_notable
		optionarg main.en_format
	endoptions
END

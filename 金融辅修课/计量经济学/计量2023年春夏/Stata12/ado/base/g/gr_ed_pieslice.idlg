/*
  gr_ed_pieslice.idlg

*!  VERSION 1.0.0  18jan2007

*/

SCRIPT pie_hide_explode
BEGIN
	pie.ck_explode.hide
	pie.tx_explode.hide
	pie.cb_explode.hide
END

SCRIPT pie_PREINIT
BEGIN
	create DOUBLE plotCustomLabel
	create DOUBLE plotCustomStyle

	create STRING fill_color
	create STRING fill_rgb
	create STRING intensity
	create STRING line_color
	create STRING line_rgb
	create STRING width
	create STRING pattern
	create DOUBLE explodeValue
	create STRING explodeStyle
	create DOUBLE drawview
	program pie_get_defaults
END

SCRIPT pie_POSTINIT
BEGIN
	program pie_initialize_controls
END

DIALOG pie, label("Pie slice properties") tabtitle("Main")
BEGIN
  GROUPBOX gb_rect	_lft	_top	_iwd	_ht17,		///
	label("Slice properties")
  TEXT     tx_fclr	_ilft	_ss	_wd	.,		///
	label("Color:") right
  COMBOBOX cb_fclr	_sep	@	_cbwd	.,		///
	dropdownlist contents(_colors) 				///
	onselchangelist(pie_cl_fclr_sel)
  COLOR    cl_fclr	_cbsep	@	_clwd	.,

  TEXT     tx_intensity	_ilft	_mls	_wd	.,		///
	label("Fill intensity:") right
  COMBOBOX cb_intensity	_sep	@	_cbwd	.,		///
	dropdown contents(_intensity)
  
  TEXT     tx_width	_ilft	 _mls	_wd	.,		///
	label("Outline width:") right
  COMBOBOX cb_width	_sep	@	_cbwd	.,		///
	dropdown 						///
	contents(_linewidths)

  TEXT     tx_pattern	_ilft	_mls	_wd	.,		///
	label("Outline pattern:") right
  COMBOBOX cb_pattern	_sep	@	_cbwd	.,		///
	dropdownlist						///
	contents(_linepatterns)

  CHECKBOX ck_outline	_ilft	_ls	_ibwd	_ht2h,		///
	groupbox						///
	label("Different outline color")			///
	onclickon(script pie_outline_on)			///
	onclickoff(script pie_outline_off) nomemory

  TEXT     tx_lclr	_iilft	_ss	_wd2	.,		///
	label("Outline color:") right
  COMBOBOX cb_lclr	_sep2	@	_cbwd	.,		///
	dropdownlist contents(_colors) 				///
	onselchangelist(pie_cl_lclr_sel)
  COLOR    cl_lclr	_cbsep	@	_clwd	.,

  CHECKBOX ck_explode	_lft	+60	_iwd	_ht2h,		///
	groupbox label("Explode slice")				///
	onclickon(program pie_check_explode)			///
	onclickoff(program pie_check_explode)
  TEXT     tx_explode	_ilft	_ss	_wd	.,		///
	label("Distance:") right
  COMBOBOX cb_explode	_sep	@	_cbwd	.,		///
	dropdown contents(_textsizes)

  // public scope (do not change name)
  CHECKBOX ck_hide	_lft	_xxls	_iwd	.,		///
	label("Hide selected slice")
END

PROGRAM pie_check_explode
BEGIN
	if pie.ck_explode {
		call pie.tx_explode.enable
		call pie.cb_explode.enable
	}
	else {
		call pie.tx_explode.disable
		call pie.cb_explode.disable
	}
END

SCRIPT pie_outline_on
BEGIN
	pie.tx_lclr.enable
	pie.cb_lclr.enable
END

SCRIPT pie_outline_off
BEGIN
	pie.tx_lclr.disable
	pie.cb_lclr.disable
END

LIST pie_cl_fclr_sel
BEGIN
	pie.cl_fclr.show
	pie.cl_fclr.hide
END
LIST pie_cl_lclr_sel
BEGIN
	pie.cl_lclr.show
	pie.cl_lclr.hide
END

PROGRAM pie_get_defaults
BEGIN
	call G.withvalue index.setvalue class @.style.shadestyle.color.styledex
	call index.withvalue fill_color.setvalue class .__STYLES.color[@]
	call pie.cb_fclr.setdefault class fill_color.value
	call G.withvalue fill_rgb.setvalue class @.style.shadestyle.color.rgb
	call pie.cl_fclr.setdefault class fill_rgb.value

	call G.withvalue index.setvalue  class @.style.shadestyle.intensity.styledex
	call index.withvalue intensity.setvalue class .__STYLES.intensity[@]
	if ! intensity {
		call G.withvalue intensity.setvalue class @.style.shadestyle.intensity.val
	}
	call pie.cb_intensity.setdefault class intensity.value

	call G.withvalue index.setvalue class @.style.linestyle.color.styledex
	call index.withvalue line_color.setvalue class .__STYLES.color[@]
	call pie.cb_lclr.setdefault class line_color.value
	call G.withvalue line_rgb.setvalue class @.style.linestyle.color.rgb
	call pie.cl_lclr.setdefault class line_rgb.value

	call G.withvalue index.setvalue class @.style.linestyle.width.styledex
	call index.withvalue width.setvalue class .__STYLES.linewidth[@]
	if ! width {
		call G.withvalue width.setvalue class @.style.linestyle.width.val
	}
	call pie.cb_width.setdefault class width.value

	call G.withvalue index.setvalue class @.explode.styledex
	call index.withvalue explodeStyle.setvalue class .__STYLES.gsize[@]
	if ! explodeStyle {
		call G.withvalue explodeStyle.setvalue class @.explode.val
	}
	call G.withvalue explodeValue.setvalue class @.explode.val
	if explodeValue.iseq(0) {
		call explodeStyle.setvalue "Medium"
		call pie.ck_explode.setdefault 0
	}
	else {
		call pie.ck_explode.setdefault 1
	}
	call pie.cb_explode.setdefault class explodeStyle.value

	call G.withvalue index.setvalue class @.style.linestyle.pattern.styledex
	call index.withvalue pattern.setvalue class .__STYLES.linepattern[@]
	call pie.cb_pattern.setdefault class pattern.value

	call G.withvalue drawview.setvalue class @.draw_view.istrue
	if drawview {
		call pie.ck_hide.setdefault 0
	}
	else {
		call pie.ck_hide.setdefault 1
	}
END

PROGRAM pie_initialize_controls
BEGIN
	if fill_color {
		call pie.cb_fclr.setvalue class fill_color.value
	}
	else {
		call pie.cb_fclr.setvalue "custom"
	}
	call pie.cl_fclr.setvalue class fill_rgb.value
	call pie.cb_intensity.setvalue class intensity.value

	if line_color {
		call pie.cb_lclr.setvalue class line_color.value
	}
	else {
		call pie.cb_lclr.setvalue "custom"
	}
	call pie.cl_lclr.setvalue class line_rgb.value

	call pie.cb_width.setvalue class width.value
	call pie.cb_pattern.setvalue class pattern.value

	if fill_rgb.isneq(line_rgb) {
		call pie.ck_outline.seton
	}
	else {
		call pie.ck_outline.setoff
	}

	if explodeValue.iseq(0) {
		call pie.ck_explode.setoff
	}
	else {
		call pie.ck_explode.seton
	}
	call pie.cb_explode.setvalue class explodeStyle.value

	if drawview {
		call pie.ck_hide.setoff
	}
	else {
		call pie.ck_hide.seton
	}
END

PROGRAM pie_output
BEGIN
	call program pie_get_defaults

	if ! pie.cl_fclr.isvisible() & ! pie.cb_fclr.isdefault() {
		put "style(shadestyle(color(" 
		put pie.cb_fclr 
		put "))) "
	}
	if pie.cl_fclr.isvisible() & ! pie.cl_fclr.isdefault() {
		put "style(shadestyle(color("  `"""'
		put pie.cl_fclr
		put `"""'
		put "))) "
	}
	if ! pie.ck_outline & pie.cb_fclr.isneq(line_color) {
		if ! pie.cl_fclr.isvisible() {
			put "style(linestyle(color(" 
			put pie.cb_fclr 
			put "))) "
		}
		if pie.cl_fclr.isvisible() & pie.cl_fclr.isneq(line_rgb) {
			put "style(linestyle(color("  `"""'
			put pie.cl_fclr
			put `"""'
			put "))) "
		}
	}
	if pie.cb_lclr & pie.ck_outline {
		if ! pie.cl_lclr.isvisible() & ! pie.cb_lclr.isdefault() {
			put "style(linestyle(color(" 
			put pie.cb_lclr 
			put "))) "
		}
		if pie.cl_lclr.isvisible() & ! pie.cl_lclr.isdefault() {
			put "style(linestyle(color("  `"""'
			put pie.cl_lclr
			put `"""'
			put "))) "
		}
	}

	if ! pie.cb_intensity.isdefault() & pie.cb_intensity {
		put "style(shadestyle(intensity("
		put pie.cb_intensity
		put "))) "
	}

	if ! pie.cb_width.isdefault() {
		put "style(linestyle(width("
		put pie.cb_width
		put "))) "
	}
	if ! pie.cb_pattern.isdefault() {
		put "style(linestyle(pattern("
		put pie.cb_pattern
		put "))) "
	}
	if pie.ck_explode.isvisible() {
		if ! pie.ck_explode.isdefault() {
			if ! pie.ck_explode {
				put "set(explode 0) "
			}
		}
		if pie.ck_explode {
			if ! pie.cb_explode.isdefault() {
				put "set(explode "
				put pie.cb_explode
				put ") "
			}
		}
	}
	if ! pie.ck_hide.isdefault() {
		if pie.ck_hide {
			put "set(draw_view no) "
		}
		else {
			put "set(draw_view yes) "
		}
	}
END

/*
    gr_line_options.dlg
    
*!  VERSION 1.0.2  07may2007

	// SCRIPTS and PROGRAMS for external use
		* SCRIPT  gr_line_setOptionsTo_cl
		* SCRIPT  gr_line_setOptionsTo_bl

*/

VERSION 10.0

INCLUDE _std_vsmall
DEFINE _dlght 175
INCLUDE header_gr_child

DEFINE _clwd	15
DEFINE _wd	140
DEFINE _sep	+145
DEFINE _cbwd	130
DEFINE _cbsep	+135

HELP hlp1, view("help cline_options")
RESET res1

SCRIPT gr_line_setOptionsTo_cl
BEGIN
	main.cb_clr.setoption clcolor
	main.cl_clr.setoption clcolor
	main.cb_width.setoption clwidth
	main.cb_pattern.setoption clpattern
	clColorSet.settrue
END

SCRIPT gr_line_setOptionsTo_bl
BEGIN
	main.cb_clr.setoption blcolor
	main.cl_clr.setoption blcolor
	main.cb_width.setoption blwidth
	main.cb_pattern.setoption blpattern
	blColorSet.settrue
END

PROGRAM main_PREINIT_PROGRAM
BEGIN
	call create PBOOLEAN blColorSet
	call create PBOOLEAN clColorSet
	if blColorSet {
		call script gr_line_setOptionsTo_bl
	}
	if clColorSet {
		call script gr_line_setOptionsTo_cl
	}
END

DIALOG main, label("Line properties")
BEGIN
  TEXT     tx_clr	_lft	20	_wd	.,		///
	label("Color:") right
  COMBOBOX cb_clr	_sep	@	_cbwd	.,		///
	dropdownlist contents(colors) option(lcolor)		///
	onselchangelist(main_cl_clr_sel)
  COLOR    cl_clr	_cbsep	@	_clwd	., option(lcolor)
  
  TEXT     tx_width	_lft	 _mls	_wd	.,		///
	label("Width:") right
  COMBOBOX cb_width	_sep	@	_cbwd	.,		///
	dropdown option(lwidth)					///
	contents(linewidths)

  TEXT     tx_pattern	_lft	_mls	_wd	.,		///
	label("Pattern:") right
  COMBOBOX cb_pattern	_sep	@	_cbwd	.,		///
	dropdownlist option(lpattern)				///
	contents(linepatterns) 	
	
  TEXT     tx_type	_lft	_mls	_wd	.,		///
	label("Connecting method:") right
  COMBOBOX cb_type	_sep	@	_cbwd	.,		///
	dropdownlist option(connect)				///
	contents(connecttypes)

  TEXT     tx_cmiss	_lft	_mls	_wd	.,		///
	label("Missing values:") right
  COMBOBOX cb_cmiss	_sep	@	_cbwd	.,		///
	dropdownlist 						///
	option(cmissing)					///
	contents(cmiss_contents) 	 			///
	values(cmiss_values)
END

LIST main_cl_clr_sel
BEGIN
	main.cl_clr.hide
	main.cl_clr.show
	main.cl_clr.hide
END

LIST cmiss_contents
BEGIN
	Default
	Fill
	No fill
END

LIST cmiss_values
BEGIN
	""
	y
	n
END

PROGRAM command
BEGIN
	if main.cl_clr.isvisible() {
		optionarg /quoted main.cl_clr
	}
	if ! main.cl_clr.isvisible() {
		optionarg /hidedefault main.cb_clr
	}
	optionarg /hidedefault main.cb_width
	optionarg /hidedefault main.cb_pattern
	optionarg /hidedefault main.cb_type
	optionarg /hidedefault main.cb_cmiss
END

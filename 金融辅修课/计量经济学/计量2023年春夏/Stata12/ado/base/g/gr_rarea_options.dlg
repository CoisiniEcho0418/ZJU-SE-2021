/*
    gr_rarea_options.dlg
    
*!  VERSION 1.0.1  13apr2007

	// SCRIPTS and PROGRAMS for external use
		* SCRIPT  gr_rarea_setOptionsTo_bl
		* SCRIPT  gr_rarea_setOrientationOff

*/

VERSION 10

INCLUDE _std_vsmall
DEFINE _dlght 220
INCLUDE header_gr_child

DEFINE _clwd	15
DEFINE _wd	140
DEFINE _sep	+145
DEFINE _cbwd	130
DEFINE _cbsep	+135

HELP hlp1, view("help twoway_rarea")
RESET res1

SCRIPT gr_rarea_setOptionsTo_bl
BEGIN
	main.cb_clr.setoption blcolor
	main.cl_clr.setoption blcolor
	main.cb_width.setoption blwidth
	main.cb_pattern.setoption blpattern
	blColorSet.settrue
END

SCRIPT gr_rarea_setOrientationOff
BEGIN
	main.tx_orient.hide
	main.cb_orient.hide
	orientationOff.settrue
END

PROGRAM PREINIT_PROGRAM
BEGIN
	call create PBOOLEAN blColorSet
	call create PBOOLEAN orientationOff
	
	if blColorSet {
		call script gr_rbar_setOptionsTo_bl
	}
	if orientationOff {
		call script gr_rbar_setOrientationOff
	}
END

DIALOG main, label("Range area options")
BEGIN
  TEXT     tx_fclr	_lft	20	_wd	.,		///
	label("Fill color:") right
  COMBOBOX cb_fclr	_sep	@	_cbwd	.,		///
	dropdownlist contents(colors) option(fcolor)		///
	onselchangelist(main_cl_fclr_sel)
  COLOR    cl_fclr	_cbsep	@	_clwd	., option(fcolor)

  TEXT     tx_intensity	_lft	_mls	_wd	.,		///
	label("Fill intensity:") right
  EDIT     ed_intensity	_sep	@	_cbwd	.,		///
	option(fintensity)

  TEXT     tx_clr	_lft	_mls	_wd	.,		///
	label("Outline color:") right
  COMBOBOX cb_clr	_sep	@	_cbwd	.,		///
	dropdownlist contents(colors) option(lcolor)		///
	onselchangelist(main_cl_clr_sel)
  COLOR    cl_clr	_cbsep	@	_clwd	., option(lcolor)

  TEXT     tx_width	_lft	_mls	_wd	.,		///
	label("Outline width:") right
  COMBOBOX cb_width	_sep	@	_cbwd	.,		///
	dropdown option(lwidth)					///
	contents(linewidths)

  TEXT     tx_pattern	_lft	_mls	_wd	.,		///
	label("Outline pattern:") right
  COMBOBOX cb_pattern	_sep	@	_cbwd	.,		///
	dropdownlist option(lpattern)				///
	contents(linepatterns) 

  TEXT     tx_orient	_lft	_mls	_wd	.,		///
	label("Orientation:") right
  COMBOBOX cb_orient	_sep	@	_cbwd	.,		///
	dropdownlist 						///
	contents(orientation_contents)  			///
	values(orientation_values)

  TEXT     tx_cmiss	_lft	_mls	_wd	.,		///
	label("Missing values:") right
  COMBOBOX cb_cmiss	_sep	@	_cbwd	.,		///
	dropdownlist 						///
	option(cmissing)					///
	contents(cmiss_contents) 	 			///
	values(cmiss_values)
END

LIST main_cl_clr_sel
BEGIN
	main.cl_clr.hide
	main.cl_clr.show
	main.cl_clr.hide
END

LIST main_cl_fclr_sel
BEGIN
	main.cl_fclr.hide
	main.cl_fclr.show
	main.cl_fclr.hide
END

LIST orientation_contents
BEGIN
	Default
	Horizontal
	Vertical
END

LIST orientation_values
BEGIN
	""
	horizontal
	vertical
END

LIST cmiss_contents
BEGIN
	Default
	Fill
	No fill
END

LIST cmiss_values
BEGIN
	""
	y
	n
END

PROGRAM command
BEGIN
	if main.cl_fclr.isvisible() {
		optionarg /quoted main.cl_fclr
	}
	if ! main.cl_fclr.isvisible() {
		optionarg /hidedefault main.cb_fclr
	}
	optionarg main.ed_intensity

	if main.cl_clr.isvisible() {
		optionarg /quoted main.cl_clr
	}
	if ! main.cl_clr.isvisible() {
		optionarg /hidedefault main.cb_clr
	}	
	optionarg /hidedefault main.cb_width
	optionarg /hidedefault main.cb_pattern
	
	put " " main.cb_orient
	optionarg /hidedefault main.cb_cmiss
END

/*
	ipolate 

*!  VERSION 1.1.0  07feb2007

*/

VERSION 8.0

INCLUDE _std_medium
INCLUDE header
HELP hlp1, view("help ipolate")
RESET res1

DIALOG main, label("ipolate - Linearly interpolate (extrapolate) values") tabtitle("Main")
BEGIN
	GROUPBOX gb_ipolate     _lft	_top	_iwd    _ht4,		/*
		*/ label("Interpolate/extrapolate")

	RADIO    rb_ipolate     _indent _ss     _inwd   ., first 	/*
		*/ label("Interpolate data")				/*
		*/ option(NONE)

	RADIO    rb_epolate     @       _ss     @       ., last 	/*
		*/ label("Interpolate and extrapolate data")		/*
		*/ option(epolate)

        TEXT 	tx_yvar		_lft	_xls	_iwd	.,		/*
     	        */ label("Y variable:")

	VARNAME vn_yvar		@	_ss	_vnwd	.,		/*
	   	*/ label("Y variable")					/*
		*/ option(yvar)
	
	TEXT	tx_xvar		@	_ms	_iwd	.,		/*
        	*/ label("X variable:")
 	
	VARNAME vn_xvar		@	_ss	_vnwd	.,		/*
	        */ label("X variable")					/*
		*/ option(xvar)
	
	TEXT	tx_gen		_lft	_ms	_iwd	.,		/*
	*/ label("Generate new variable: (equal to Y with missings replaced)")

	EDIT	ed_gen		@	_ss	_vnwd	.,		/*
		*/ label("Generate new variable")			/*
		*/ max(32)						/*
		*/ option(generate)
END

INCLUDE byifin	

PROGRAM command
BEGIN
	INCLUDE _by_pr
	put "ipolate "
	varlist main.vn_yvar
	varlist main.vn_xvar
	require main.ed_gen
	INCLUDE _ifin_pr
	beginoptions
		optionarg main.ed_gen
		option radio(main rb_ipolate rb_epolate)
	endoptions
END

/*
	lsens_9

*!  VERSION 1.1.3  20aug2006  (deprecated)

*/

VERSION 9.0

INCLUDE graph_header

INCLUDE header

HELP hlp1, view("help lsens")
RESET res1

DIALOG main, tabtitle("Main")	///
	label("lsens - Graph sensitivity and specificity versus probability cutoff")
BEGIN
  GROUPBOX gb_test        _lft      _top      _iwd      _ht4,	///
	label("Graph sensitivity and specificity versus probability cutoff for:")
  RADIO    rb_estimate    _ilft     _ss       _ibwd     .,	///
	label("Estimation sample")      			///
	first							///
	onclickon(program enable_sub)

  RADIO    rb_all         @         _ss       @         .,	///
	label("All observations in the data")			///
	last							///
	onclickon(program disable_sub)				///
	option(all)
  GROUPBOX gb_opt         _lft      _xls      _iwd      _ht18h,	///
	label("Options")
  TEXT     tx_genprob     _ilft     _ss       _ibwd     .,	///
	label("Create variable containing probability cutoffs:")
  EDIT     vn_genprob     @         _ss       _vnwd     .,	///
	label("Probability cutoffs variable")			///
	option(genprob)
  TEXT     tx_gensens     @         _ms       _ibwd     .,	///
	label("Create variable containing sensitivity:")
  EDIT     vn_gensens     @         _ss       _vnwd     .,	///
	label("Sensitivity variable")				///
	option(gensens)
  TEXT     tx_genspec     @         _ms       _ibwd     .,	///
	label("Create variable containing specificity:")
  EDIT     vn_genspec     @         _ss       _vnwd     .,	///
	label("Specificity variable")				///
	option(genspec)
  CHECKBOX ck_replace     @         _ls       _ibwd     .,	///
	label("Overwrite existing variables")			///
	option(replace) nomem
  CHECKBOX ck_nograph     @         _ms       @         .,	///
	label("Suppress graph")					///
	onclickoff(script graph_on)				///
	onclickon(script graph_off)				///
	option(nograph)
END

SCRIPT graph_on
BEGIN
	/* turn on all the graphs tabs */
	script p1_opts_enable
	script addplot_enable
	script gr_twoway_enable
END

SCRIPT graph_off
BEGIN
	/* turn off all the graphs tabs */
	script p1_opts_disable
	script addplot_disable
	script gr_twoway_disable
END

INCLUDE ifin
INCLUDE weights_f

DIALOG adv, tabtitle("Advanced")
BEGIN
  TEXT     tx_depvar      _ilft     _top      _iwd      .,		///
	label("Dependent variable:")
  VARNAME  vn_depvar      @         _ss       _vnwd     .,		///
	label("Dependent variable")
  TEXT     tx_beta        @         _ms       _iwd      .,		///
	label("Vector containing coefficients for the model:")
  LISTBOX  lb_beta        @         _ss       _inwd     .,		///
	label("Vector containing coefficients for the model")		///
	option(beta)							///
	contents(row)
END

INCLUDE p1_opts
INCLUDE addplot
INCLUDE gr_twoway

SCRIPT PREINIT
BEGIN
	script p1_opts_createAsNoRange
	script p1_opts_setDefaultConnected
END

PROGRAM command
BEGIN
        if adv.vn_depvar {
		require adv.lb_beta
        }
	put "lsens "
        varlist [adv.vn_depvar]
	INCLUDE _weights_pr
	INCLUDE _ifin_pr
	beginoptions
		option main.rb_all
		optionarg main.vn_genprob
		optionarg main.vn_gensens
		optionarg main.vn_genspec
		option main.ck_replace
		option main.ck_nograph
                if adv.vn_depvar & adv.lb_beta {
                        optionarg adv.lb_beta
                }

		if ! main.ck_nograph {
			put " " /program p1_opts_output
			put " " /program addplot_output
			put " " /program gr_twoway_output
		}
	endoptions
END
/* end */

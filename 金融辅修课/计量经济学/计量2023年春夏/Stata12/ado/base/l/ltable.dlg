/*
    ltable.dlg

*!  VERSION 2.0.5  22jul2008

*/

VERSION 10.0

INCLUDE _std_xwide
DEFINE _dlght 360
INCLUDE header
HELP hlp1, view("help ltable")
RESET res1

SCRIPT POSTINIT
BEGIN
	script gr_plots_setDefaultConnected
	script gr_plots_setNoMarkerLabel
	script gr_ciplots_setDefaultSpike
	script gr_ciplots_setNoMarkerLabel
	gr_ciplots_outputCmd.setstring "ciopts"
	gr_plots_outputCmd.setstring "plotopts"
END

DIALOG main, label("ltable - Life tables for survival data") tabtitle("Main")
BEGIN
  TEXT     tx_tvar   _lft      _top      _cwd4_1   .,		/*
		*/ label("Time variable:")

  VARNAME  vn_tvar   @         _ss       _vnwd     .,		/*
		*/ label("Time variable")

  TEXT     tx_dvar   _lft4_2   _top      _cwd4_1   .,		/*
		*/ label("Failure variable:")

  VARNAME  vn_dvar   @         _ss       _vnwd	   .,    	/*
		*/ label("Failure variable")

  GROUPBOX gb_output _lft      +35       _cwd1     _ht6,	/*
		*/ label("Type of result")
  RADIO    rb_table  _ilft     _ss       _inwd     .,		/*
  		*/ label("Table") 				/*
  		*/ onclickon(script main_rb_table_on) first
  RADIO    rb_graph  @         _ss       @         .,		/*
  		*/ label("Graph") 				/*
  		*/ onclickon(program main_rb_graph_on) 		/*
  		*/ option(graph notable)
  RADIO    rb_both   @         _ss       @         .,		/*
  		*/ label("Graph and table") 			/*
  		*/ onclickon(script main_rb_both_on) last	/*
  		*/ option(graph)

  GROUPBOX gb_funct  _lft      _xls      _cwd1     _ht7,	/*
  		*/ label("Function")
  CHECKBOX ck_surv   _indent   _ss       _inwd     .,		/*
		*/ option(survival)				/*
		*/ label("Survival table")			/*
		*/ default(1)
DEFINE   holdy @y
  CHECKBOX ck_fail   @         _ms       @         .,		/*
		*/ option(failure)				/*
		*/ label("Cumulative failure table")
  CHECKBOX ck_haz    @         _ms       @         .,		/*
		*/ option(hazard)				/*
		*/ label("Hazard table")
  RADIO    rb_surv   @         holdy     @         .,		/*
  		*/ first option(survival) 			/*
  		*/ label("Survival function")
  RADIO    rb_fail   @         _ss       @         .,		/*
  		*/ last option(failure)				/*
  		*/ label("Cumulative failure function")
  CHECKBOX ck_ci   @         _ls       @         .,		/*
		*/ option(ci)					/*
		*/ onclickon(program ci_check)			/*
		*/ onclickoff(program ci_check)			/*
		*/ label("Graph confidence intervals")

  DEFINE _x _lft
  DEFINE _y _xxls
  DEFINE _cx _spr2
  INCLUDE _sp_level

  CHECKBOX ck_noadj  _lft      _ls       _cwd1     .,		/*
		*/ option(noadjust)				/*
		*/ label("Suppress actuarial adjustment for deaths and")
  TEXT     tx_noadj  _indent2  _vss	 _cwd3	   .,		/*
  		*/ label("censored observations")

  CHECKBOX ck_by     _lft2     _top      _cwd1     _ht11h,	/*
  		*/ groupbox					/*
  		*/ onclickon(program main_ck_by_on)		/*
  		*/ onclickoff(program main_ck_by_off)		/*
  		*/ label("Group tables or graphs by values of a variable")
  TEXT     tx_by     _indent   _ms       _cwd3     .,		/*
  		*/ label("Variable:")
DEFINE holdx @x
  VARNAME  vn_by     @         _ss       _vnwd     .,		/*
  		*/ label("Grouping variable") option(by)
  CHECKBOX ck_test   @         _ls       _cwd3 	   .,		/*
  		*/ option(test)					/*
  		*/ label("Report chi-squared measure of differences")
  TEXT     tx_test   _cksep    _vss      _cwd5     .,		/*
  		*/ label("between groups (two tests)")
  CHECKBOX ck_over   holdx     _ms       _cwd3     .,		/*
  		*/ label("Overlay plots on the same graph") 	/*
  		*/ option(over) 				/*
  		*/ onclickon(program main_overlay_check)	/*
  		*/ onclickoff(program main_overlay_check)

  GROUPBOX gb_opts   _lft2     _xls      _cwd1     _ht15,	/*
  		*/ label("Additional options")
  TEXT     tx_tvid   _indent   _ms       _inwd     .,		/*
		*/ label("Subject ID variable: (when time varying)")
DEFINE width @xsize
  VARNAME  vn_tvid   @         _ss       _vnwd     .,		/*
		*/ option(tvid)					/*
		*/ label("Subject ID variable")
  TEXT     tx_int    @         _ls       width     .,		/*
		*/ label("Time intervals:")
  EDIT     ed_int    @         _ss       @         .,		/*
		*/ option(intervals)				/*
		*/ label("Time intervals")
  TEXT     tx_sav    @         _ls       @         .,			/*
		*/ label("Save life table data to file:")		/*
		*/
  FILE     fi_sav    @         _ss       @         .,			/*
		*/ label("Browse...")					/*
		*/ option("saving")					/*
		*/ filter("Stata Dataset (*.dta)|*.dta|All (*.*)|*.*")	/*
		*/ defext(dta)						/*
		*/ save							/*
		*/
END

PROGRAM main_overlay_check
BEGIN
	if ! main.ck_over.isenabled() {
		exit
	}
	if main.ck_over & main.ck_over.isenabled() {
		call script gr_plots_setMultiView_on
		call script gr_ciplots_setMultiView_on
		if ! main.rb_table {
			call script gr_addplots_enable
		}
		call script main_byContext_off
	}
	else {
		call script gr_plots_setMultiView_off
		call script gr_ciplots_setMultiView_off
		if ! main.rb_table {
			call script gr_addplots_disable
		}
		if main.ck_by {
			call script main_byContext_on
		}
	}
END

SCRIPT main_byContext_on
BEGIN
	script gr_legend_setByoptsContext_on
	script gr_titles_setByoptsContext_on
	script gr_yaxis_setByoptsContext_on
	script gr_xaxis_setByoptsContext_on
	script gr_overall_setByoptsContext_on
END
SCRIPT main_byContext_off
BEGIN
	script gr_legend_setByoptsContext_off
	script gr_titles_setByoptsContext_off
	script gr_yaxis_setByoptsContext_off
	script gr_xaxis_setByoptsContext_off
	script gr_overall_setByoptsContext_off
END

SCRIPT main_rb_table_on
BEGIN
	main.rb_surv.hide
	main.rb_fail.hide
	main.ck_ci.hide
	main.ck_surv.show
	main.ck_fail.show
	main.ck_haz.show
	main.ck_over.disable
	script gr_plots_disable
	script gr_addplots_disable
	script gr_xaxis_disable
	script gr_yaxis_disable
	script gr_titles_disable
	script gr_legend_disable
	script gr_overall_disable
	program ci_check
	main.ck_by.setlabel "Group tables by values of a variable"
END

PROGRAM main_rb_graph_on
BEGIN
	call program main_graph_common
	call main.ck_by.setlabel "Group graphs by values of a variable"
END

SCRIPT main_rb_both_on
BEGIN
	program main_graph_common
	main.ck_by.setlabel "Group graphs and tables by values of a variable"
END

PROGRAM main_graph_common
BEGIN
	call main.rb_surv.show
	call main.rb_fail.show
	call main.ck_ci.show
	call main.ck_surv.hide
	call main.ck_fail.hide
	call main.ck_haz.hide
	if main.ck_by {
		call main.ck_over.enable
		if main.ck_over.isenabled() & main.ck_over {
			call script gr_addplots_enable
		}
		else {
			call script gr_addplots_disable
		}
	}
	else {
		call script gr_addplots_enable
	}
	call script gr_plots_enable
	call script gr_xaxis_enable
	call script gr_yaxis_enable
	call script gr_titles_enable
	call script gr_legend_enable
	call script gr_overall_enable
	call program ci_check
END

PROGRAM ci_check
BEGIN
	if ( main.ck_ci &			///
	    (main.rb_graph | main.rb_both)) {
		call script gr_ciplots_enable
	}
	else {
		call script gr_ciplots_disable
	}
END

PROGRAM main_ck_by_on
BEGIN
	call main.tx_by.enable
	call main.vn_by.enable
	if main.rb_graph | main.rb_both {
		call main.ck_over.enable
		if main.rb_table & main.ck_over {
			call script gr_addplots_enable
		}
		else {
			call script gr_addplots_disable
		}
	}
	call main.ck_test.enable
	call main.tx_test.enable
	call program main_overlay_check
END

PROGRAM main_ck_by_off
BEGIN
	call main.tx_by.disable
	call main.vn_by.disable
	call main.ck_over.disable
	call main.ck_test.disable
	call main.tx_test.disable
	if main.rb_graph | main.rb_both {
		call script gr_addplots_enable
	}
	call script gr_plots_setMultiView_off
	call script gr_ciplots_setMultiView_off
	call script main_byContext_off
END

INCLUDE ifin
INCLUDE weights_f
INCLUDE gr_plots
INCLUDE gr_ciplots
INCLUDE gr_addplots_2
INCLUDE gr_yaxis
INCLUDE gr_xaxis
INCLUDE gr_titles
INCLUDE gr_legend
INCLUDE gr_overall

PROGRAM command
BEGIN
	put "ltable "
	varlist main.vn_tvar
	varlist [main.vn_dvar]
	put " " /program weights_output
	put " " /program ifin_output
	beginoptions
		option main.ck_surv
		option main.ck_fail
		option main.ck_haz
		option main.rb_graph
		option main.rb_both
		option main.rb_surv
		option main.rb_fail
		option main.ck_ci
		INCLUDE _level_main_pr
		if main.ck_by {
			require main.vn_by
			optionarg main.vn_by
		}
		option main.ck_noadj
		option main.ck_test
		option main.ck_over
		optionarg main.vn_tvid
		optionarg main.ed_int
		if main.fi_sav {
			repfile main.fi_sav
			put `"saving(""'
			put main.fi_sav
			put `"", replace)"'
		}

		put " " /program gr_plots_output
		put " " /program gr_ciplots_output
		put " " /program gr_addplots_output
		put " " /program gr_yaxis_output
		put " " /program gr_xaxis_output
		put " " /program gr_titles_output
		put " " /program gr_legend_output
		put " " /program gr_overall_output
	endoptions
END

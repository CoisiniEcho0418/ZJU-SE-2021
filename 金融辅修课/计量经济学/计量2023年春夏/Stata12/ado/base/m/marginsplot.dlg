/*
    marginsplot.dlg

*!  VERSION 1.0.3  17may2011

*/

VERSION 12.0
INCLUDE _std_wide
DEFINE _dlght 340
INCLUDE header

HELP hlp1, view("help marginsplot")
RESET res1

SCRIPT PREINIT
BEGIN
	create ARRAY main_dim_variables
	create STRING main_e_cmd
	create STRING main_e_cmd2
	create STRING main_e_marg_dims
	create STRING main_r_cmd
	create STRING main_r_cmd2
	create STRING main_r_marg_dims

	main_e_cmd.setstring e(cmd)
	main_e_cmd2.setstring e(cmd2)
	main_e_marg_dims.setstring e(marg_dims)

	main_r_cmd.setstring r(cmd)
	main_r_cmd2.setstring r(cmd2)
	main_r_marg_dims.setstring r(marg_dims)

	create STRING bu_xdimOptResults
	create STRING bu_pdimOptResults
	create STRING bu_bdimOptResults
	create STRING bu_gdimOptResults
END

PROGRAM POSTINIT_PROGRAM
BEGIN
	if main_e_cmd.iseq("margins") | main_e_cmd2.iseq("margins") {
		if main_e_marg_dims.isneq("") {
			call main_e_marg_dims.tokenize main_dim_variables
			call main.cb_xdimension.repopulate
			call main.cb_plotdimension.repopulate
			call main.cb_bydimension.repopulate
			call main.cb_graphdimension.repopulate
		}
	}

	if main_r_cmd.iseq("margins") | main_r_cmd2.iseq("margins") {
		if main_r_marg_dims.isneq("") {
			call main_r_marg_dims.tokenize main_dim_variables
			call main.cb_xdimension.repopulate
			call main.cb_plotdimension.repopulate
			call main.cb_bydimension.repopulate
			call main.cb_graphdimension.repopulate
		}
	}
	call script gr_overall_setName_off
END

DIALOG main, tabtitle("Main")						///
	label("marginsplot - Graph results from margins")
BEGIN
  GROUPBOX gb_dimension		_lft	_top	_iwd	_ht20,		///
	label("Dimensions")
  TEXT tx_xdimension		_ilft	_ss	_inwd	.,		///
	label("Variable(s) that define the x axis:")
  DEFINE hold_xs @xsize
  COMBOBOX cb_xdimension	@	_ss	_lw100	.,		///
	append								///
	dropdown							///
	nomemory							///
	contents(main_dim_variables)					///
	label("Variable(s) that define the x axis")
  BUTTON  bu_xdimension		_slw100	@	_buwd	.,		///
	onpush(script showXDimOptsDialog)				///
	tooltip("Dimension options")					///
	label("Options  ")

  TEXT tx_plotdimension		_ilft	_ls	hold_xs	.,		///
	label("Create plots for groups defined by variables:")
  COMBOBOX cb_plotdimension	@	_ss	_lw100	.,		///
	append								///
	dropdown							///
	nomemory							///
	contents(main_dim_variables)					///
	label("Create plots for groups defined by variables")
  BUTTON  bu_plotdimension	_slw100	@	_buwd	.,		///
	onpush(script showPDimOptsDialog)				///
	tooltip("Dimension options")					///
	label("Options  ")

  TEXT tx_bydimension		_ilft	_ls	hold_xs	.,		///
	label("Create subgraphs for groups defined by variables:")
  COMBOBOX cb_bydimension	@	_ss	_lw100	.,		///
	append								///
	dropdown							///
	nomemory							///
	contents(main_dim_variables)					///
	label("Create subgraphs for groups defined by variables")
  BUTTON  bu_bydimension	_slw100	@	_buwd	.,		///
	onpush(script showBDimOptsDialog)				///
	tooltip("Dimension options")					///
	label("Options  ")

  TEXT tx_graphdimension	_ilft	_ls	hold_xs	.,		///
	label("Create graphs for groups defined by variables:")
  COMBOBOX cb_graphdimension	@	_ss	_lw100	.,		///
	append								///
	dropdown							///
	nomemory							///
	contents(main_dim_variables)					///
	label("Create graphs for groups defined by variables")
  BUTTON  bu_graphdimension	_slw100	@	_buwd	.,		///
	onpush(script showGDimOptsDialog)				///
	tooltip("Dimension options")					///
	label("Options  ")

  CHECKBOX ck_horizontal	_lft	_xxls	_cwd1	.,		///
	option(horizontal)						///
	label("Swap the x and y axes (horizontal graph)")
  DEFINE hold_y @y
  CHECKBOX ck_noci		@	_ms	@	.,		///
	option(noci)							///
	onclickon(script gr_ciopts_disable)				///
	onclickoff(script gr_ciopts_enable)				///
	label("Do not plot confidence intervals")

  CHECKBOX ck_name		_lft2	hold_y	_cwd1	_ht4h,		///
	groupbox							///
	onclickon(script main_name_on)					///
	onclickoff(script main_name_off)				///
	label("Name of graph, or stub if multiple graphs")
  EDIT ed_name			_ilft2	_ss	_inwd	.,		///
	label("New name")
  CHECKBOX ck_replace		@	_ms	@	.,		///
	option(replace)							///
	nomemory							///
	label("Replace graph if it already exists in memory")
END

SCRIPT showXDimOptsDialog
BEGIN
	create CHILD marginsplot_dimopts AS xdim, allowsubmit
	xdim.settitle "x dimension label"
	xdim.setExitString bu_xdimOptResults
	xdim.setExitAction "program main_xdim_ckResults"
	xdim.setSubmitAction "script main_xdim_submit"
END

PROGRAM main_xdim_ckResults
BEGIN
	if bu_xdimOptResults {
		call main.bu_xdimension.setlabel "Options *"
	}
	else {
		call main.bu_xdimension.setlabel "Options  "
	}
END

SCRIPT main_xdim_submit
BEGIN
	program main_xdim_ckResults
	Submit
END

SCRIPT showPDimOptsDialog
BEGIN
	create CHILD marginsplot_dimopts AS pdim, allowsubmit
	pdim.settitle "Plot Dimension label"
	pdim.setExitString bu_pdimOptResults
	pdim.setExitAction "program main_pdim_ckResults"
	pdim.setSubmitAction "script main_pdim_submit"
END

PROGRAM main_pdim_ckResults
BEGIN
	if bu_pdimOptResults {
		call main.bu_plotdimension.setlabel "Options *"
	}
	else {
		call main.bu_plotdimension.setlabel "Options  "
	}
END

SCRIPT main_pdim_submit
BEGIN
	program main_pdim_ckResults
	Submit
END

SCRIPT showBDimOptsDialog
BEGIN
	create CHILD marginsplot_dimopts AS bdim, allowsubmit
	bdim.settitle "By dimension label"
	bdim.setExitString bu_bdimOptResults
	bdim.setExitAction "program main_bdim_ckResults"
	bdim.setSubmitAction "script main_bdim_submit"
END

PROGRAM main_bdim_ckResults
BEGIN
	if bu_bdimOptResults {
		call main.bu_bydimension.setlabel "Options *"
	}
	else {
		call main.bu_bydimension.setlabel "Options  "
	}
END

SCRIPT main_bdim_submit
BEGIN
	program main_bdim_ckResults
	Submit
END

SCRIPT showGDimOptsDialog
BEGIN
	create CHILD marginsplot_dimopts AS gdim, allowsubmit
	gdim.settitle "Graph dimension label"
	gdim.setExitString bu_gdimOptResults
	gdim.setExitAction "program main_gdim_ckResults"
	gdim.setSubmitAction "script main_gdim_submit"
END

PROGRAM main_gdim_ckResults
BEGIN
	if bu_gdimOptResults {
		call main.bu_graphdimension.setlabel "Options *"
	}
	else {
		call main.bu_graphdimension.setlabel "Options  "
	}
END

SCRIPT main_gdim_submit
BEGIN
	program main_gdim_ckResults
	Submit
END

SCRIPT main_name_on
BEGIN
	main.ed_name.enable
	main.ck_replace.enable
END

SCRIPT main_name_off
BEGIN
	main.ed_name.disable
	main.ck_replace.disable
END

DIALOG labels, tabtitle("Labels")
BEGIN
  CHECKBOX ck_allxlabels	_lft	_top	_iwd	.,		///
	option(allxlabels)						///
	label("Place ticks and labels on the x axis for each value")
  CHECKBOX ck_nolabels		@	_ms	@	.,		///
	option(nolabels)						///
	label("Label groups with their values, not their labels")
  CHECKBOX ck_allsimplelabels	@	_ms	@	.,		///
	option(allsimplelabels)						///
	onclickon("labels.ck_nosimplelabels.disable")			///
	onclickoff("labels.ck_nosimplelabels.enable")			///
	label("Forgo variable name and equal signs in all labels")
  CHECKBOX ck_nosimplelabels	@	_ms	@	.,		///
	option(nosimplelabels)						///
	onclickon("labels.ck_allsimplelabels.disable")			///
	onclickoff("labels.ck_allsimplelabels.enable")			///
	label("Include variable name and equal signs in all labels")
  CHECKBOX ck_separator		@	_ls	350	_ht2,		///
	groupbox							///
	onclickon("script labels_separator_on")				///
	onclickoff("script labels_separator_off")			///
	label("Separator for labels when multiple variables are specified")
  EDIT ed_separator		_indent	_ss	_inwd	.,		///
	option(separator)						///
	label("Separator for labels when multiple variables are specified")
  CHECKBOX ck_noseparator	_lft	_xls	_iwd	.,		///
	onclickon("labels.ck_separator.disable")			///
	onclickoff("labels.ck_separator.enable")			///
	option(noseparator)						///
	label("Do not use a separator between labels")
END

SCRIPT labels_separator_on
BEGIN
	labels.ed_separator.enable
	labels.ck_noseparator.disable
END

SCRIPT labels_separator_off
BEGIN
	labels.ed_separator.disable
	labels.ck_noseparator.enable
END

INCLUDE gr_plotopts
INCLUDE gr_ciopts

DIALOG pair, tabtitle("Pairwise")
BEGIN
  CHECKBOX ck_unique		_lft	_top	_iwd	.,		///
	option(unique)							///
	label("Plot only unique pairwise comparisons")
  CHECKBOX ck_csort		@	_ms	@	.,		///
	option(csort)							///
	label("Sort comparison categories first")
END

INCLUDE gr_addplots_2
INCLUDE gr_yaxis
INCLUDE gr_xaxis
INCLUDE gr_titles
INCLUDE gr_legend
INCLUDE gr_overall
INCLUDE gr_by

PROGRAM name_output
BEGIN
	put main.ed_name
	if main.ck_replace {
		put ", replace"
	}
END

PROGRAM xdimension_output
BEGIN
	if bu_xdimOptResults {
		put main.cb_xdimension
		put ", " bu_xdimOptResults
	}
	else {
		put main.cb_xdimension
	}
END

PROGRAM plotdimension_output
BEGIN
	if bu_pdimOptResults {
		put main.cb_plotdimension
		put ", " bu_pdimOptResults
	}
	else {
		put main.cb_plotdimension
	}
END

PROGRAM bydimension_output
BEGIN
	if bu_bdimOptResults {
		put main.cb_bydimension
		put ", " bu_bdimOptResults
	}
	else {
		put main.cb_bydimension
	}
END

PROGRAM graphdimension_output
BEGIN
	if bu_gdimOptResults {
		put main.cb_graphdimension
		put ", " bu_gdimOptResults
	}
	else {
		put main.cb_graphdimension
	}
END

PROGRAM command
BEGIN
	put "marginsplot"
	beginoptions
		if main.cb_xdimension | bu_xdimOptResults {
			put " xdimension("
			put /program xdimension_output
			put ")"
		}
		if main.cb_plotdimension | bu_pdimOptResults {
			put " plotdimension("
			put /program plotdimension_output
			put ")"
		}
		if main.cb_bydimension | bu_bdimOptResults {
			put " bydimension("
			put /program bydimension_output
			put ")"
		}
		if main.cb_graphdimension | bu_gdimOptResults {
			put " graphdimension("
			put /program graphdimension_output
			put ")"
		}
		option main.ck_horizontal
		option main.ck_noci
		if main.ck_name {
			require main.ed_name
			put " name("
			put /program name_output
			put ")"
		}
		option labels.ck_allxlabels
		option labels.ck_nolabels
		option labels.ck_allsimplelabels
		option labels.ck_nosimplelabels
		if labels.ck_separator {
			optionarg labels.ed_separator
			require labels.ed_separator
		}
		option labels.ck_noseparator
		option pair.ck_unique
		option pair.ck_csort
		put " " /program gr_plotopts_output
		put " " /program gr_ciopts_output
		put " " /program gr_addplots_output
		put " " /program gr_yaxis_output
		put " " /program gr_xaxis_output
		put " " /program gr_titles_output
		put " " /program gr_legend_output
		put " " /program gr_overall_output
		put " " /program gr_by_output
	endoptions
END

/*
	oneway

*!  VERSION 1.1.0  18nov2004

*/

VERSION 9.0

INCLUDE _std_large
INCLUDE _ht320
INCLUDE header

HELP hlp1, view("help oneway")
RESET res1

DIALOG main, label("oneway - One-way analysis of variance") tabtitle("Main")
BEGIN
  TEXT     tx_dv              _lft     _top    _cwd1        .,		///
	label("Response variable:")
  VARNAME  vn_dv                 @      _ss        @        .,		///
	label("Response variable")

  TEXT     tx_facvar         _lft2     _top    _cwd1        .,		///
	label("Factor variable:")
  VARNAME  vn_facvar             @      _ss        @        .,		///
	label("Factor variable")

  GROUPBOX gb_multcomp        _lft     _ls      _iwd     _ht2,		///
	label("Multiple-comparison tests")
  CHECKBOX ck_bonf         _indent      _ss  _cwd3_2        .,		///
	option(bonferroni) label("Bonferroni")
  CHECKBOX ck_scheffe     _ilft3_2        @        @        .,		///
	option(scheffe) label("Scheffe")
  CHECKBOX ck_sidak       _ilft3_3        @        @        .,		///
	option(sidak) label("Sidak")

  GROUPBOX gb_output          _lft     _xls     _iwd   _ht12h,		///
	label("Output")
  CHECKBOX ck_tabulate     _indent      _ss    _cwd2    _ht9h, 		///
	groupbox option(tabulate)					///
	label("Produce summary table")					///
	onclickon(script tabon)	onclickoff(script taboff)
  DEFINE   holdy @y
  CHECKBOX ck_nomeans      _indent      _ss    _inwd        .,		///
	option(nomeans) label("Suppress means")
  CHECKBOX ck_nostandard         @      _ms        @        .,		///
	option(nostandard) label("Suppress standard deviations")
  CHECKBOX ck_nofreq             @      _ms        @        .,		///
	option(nofreq) label("Suppress frequencies")
  CHECKBOX ck_noobs              @      _ms        @        .,		///
	option(noobs) label("Suppress number of obs.")

  CHECKBOX ck_missing         _lft    _xxls     _iwd        .,		///
	option(missing) label("Treat missing values as categories")

  GROUPBOX gb_other          _lft2    holdy    _cwd2    _ht9h,		///
	label("Other")
  CHECKBOX ck_noanova      _indent      _ss    _inwd        .,		///
	option(noanova)	label("Suppress the ANOVA table")
  CHECKBOX ck_nolabel            @      _ms        @        .,		///
	option(nolabel) label("Show numeric codes, not labels")
  CHECKBOX ck_wrap               @      _ms        @        .,		///
	option(wrap) label("Do not break wide tables")
END

INCLUDE byifin
INCLUDE weights_fa

SCRIPT tabon
BEGIN
  main.ck_nomeans.enable
  main.ck_nostandard.enable
  main.ck_nofreq.enable
  main.ck_noobs.enable
END

SCRIPT taboff
BEGIN
  main.ck_nomeans.disable
  main.ck_nostandard.disable
  main.ck_nofreq.disable
  main.ck_noobs.disable
END

PROGRAM command
BEGIN
	INCLUDE _by_pr
	put "oneway "
	varlist main.vn_dv
	varlist main.vn_facvar
	INCLUDE _weights_pr
	INCLUDE _ifin_pr
	beginoptions
		option main.ck_bonf
		option main.ck_scheffe
		option main.ck_sidak
		option main.ck_tabulate
		option main.ck_nomeans
		option main.ck_nostandard
		option main.ck_nofreq
		option main.ck_noobs
		option main.ck_missing
		option main.ck_noanova
		option main.ck_nolabel
		option main.ck_wrap
	endoptions
END

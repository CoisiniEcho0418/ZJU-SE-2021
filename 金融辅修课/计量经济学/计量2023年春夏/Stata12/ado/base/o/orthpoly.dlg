/*
  -orthpoly-

*! VERSION 1.0.1  27aug2003

*/

VERSION 8.0

INCLUDE _std_medium
INCLUDE _ht300
INCLUDE header

HELP hlp1, view("help orthpoly")
RESET res1

DEFINE exin    +60
DEFINE exina   +80
DEFINE exwd     55
DEFINE exwda   195


DIALOG main, label("orthpoly - Orthogonal polynomials")	tabtitle("Main")
BEGIN
  TEXT     tx_var            _lft      _top      _iwd      .,		/*
		*/ label("Orthogonal polynomials of variable:")
  VARNAME  vn_var            @         _ss       _vnwd     .,		/*
		*/ label("Orthogonal polynomials of variable")

  SPINNER  sp_degree         @         _ls       _spwd     .,		/*
		*/ min(1) max(100) default(1)				/*
		*/ option(degree)					/*
		*/ label("Polynomial degree")
  TEXT     tx_degree         _spsep    @         _spr      .,		/*
		*/ label("Degree  (highest-degree polynomial to include)")

  GROUPBOX gb_gen            _lft      _ls       _iwd      _ht17,	/*
		*/ label("Generate")
  RADIO    rb_gen            _ilft     _ss       _ibwd     ., first	/*
		*/ clickon("script gen")				/*
		*/ label("Variables:  (degree # of variables required)")
  EDIT     ed_gen            _indent2  _ss       _inwd     .,		/*
		*/ option(generate)					/*
		*/ label("Generate variables")
  TEXT     tx_examples       @         _ss       exwd      .,		/*
		*/ label("Examples:")
  TEXT     tx_examp1         exin      @         exwd      ., right	/*
		*/ label("v*")
  DEFINE   holdx @x
  TEXT     tx_examp1a        exina     @         exwda     .,		/*
		*/ label("creates variables v1, v2, ..., v#")
  TEXT     tx_examp2         holdx     _vss      exwd      ., right	/*
		*/ label("z1-z8")
  TEXT     tx_examp2a        exina     @         exwda     .,		/*
		*/ label("creates variables z1, z2, ..., z8")
  TEXT     tx_examp3         holdx     _vss      exwd      ., right	/*
		*/ label("a b c")
  TEXT     tx_examp3a        exina     @         exwda     .,		/*
		*/ label("creates variables a, b, c")

  RADIO    rb_poly           _ilft     _ls       _ibwd     .,		/*
		*/ clickon("script poly")				/*
		*/ label("Matrix:  (coefficients of the polynomials)")
  EDIT     ed_poly           _indent2  _ss       _vnwd     .,		/*
		*/ option(poly)						/*
		*/ label("Generate matrix")

  RADIO    rb_genpoly        _ilft     _ls       _ibwd     ., last	/*
		*/ clickon("script both")				/*
		*/ label("Variables and matrix")
END


INCLUDE weights_fpai
INCLUDE ifin


SCRIPT gen
BEGIN
  script gen_on
  main.ed_poly.disable
END

SCRIPT poly
BEGIN
  script gen_off
  main.ed_poly.enable
END

SCRIPT both
BEGIN
  script gen_on
  main.ed_poly.enable
END

SCRIPT gen_on
BEGIN
  main.ed_gen.enable
  main.tx_examples.enable
  main.tx_examp1.enable
  main.tx_examp1a.enable
  main.tx_examp2.enable
  main.tx_examp2a.enable
END

SCRIPT gen_off
BEGIN
  main.ed_gen.disable
  main.tx_examples.disable
  main.tx_examp1.disable
  main.tx_examp1a.disable
  main.tx_examp2.disable
  main.tx_examp2a.disable
END


PROGRAM command
BEGIN
	put "orthpoly "
	varlist main.vn_var
	INCLUDE _weights_pr
	INCLUDE _ifin_pr
	beginoptions
		if main.rb_gen | main.rb_genpoly {
			require main.ed_gen
			optionarg main.ed_gen
		}
		if main.rb_poly | main.rb_genpoly {
			require main.ed_poly
			optionarg main.ed_poly
		}
		optionarg main.sp_degree
	endoptions
END


/*
  pkexamine_9

*! VERSION 1.1.1  11sep2006  (deprecated)

*/

VERSION 9.0

INCLUDE graph_header
INCLUDE header

HELP hlp1, view("help pkexamine")
RESET res1

DIALOG main, label("pkexamine - Calculate pharmacokinetic measures") 	///
	tabtitle("Main")
BEGIN
  TEXT     tx_vart         _lft      _top      _cwd1     .,		///
	label("Time variable:")
  TEXT     tx_varc         _lft4_2   @         @         .,		///
	label("Concentration variable:")
  VARNAME  vn_vart         _lft      _ss       _vnwd     .,		///
	label("Time variable")
  VARNAME  vn_varc         _lft4_2   @         @         .,		///
	label("Concentration variable")
  GROUPBOX gb_opt          _lft      +35       _iwd      _ht18,		///
	label("Options")
  CHECKBOX ck_trap         _indent   _ss       _cwd4_1   .,		///
	label("Use trapezoidal rule") option(trapezoid)
  DEFINE _x @x
  CHECKBOX ck_fit          _lft4_2   @         @         .,		///
	label("Points to extend AUC:")					///
	onclickon(main.sp_fit.enable)					///
	onclickoff(main.sp_fit.disable)
  SPINNER  sp_fit          _indent2  _ss       _spwd     .,		///
	label("Number of points to use in fit")				///
	min(3) max(10000000) default(3) option(fit)
  CHECKBOX ck_graph        _x        _ms       _ibwd     _ht10h,	///
	option(graph) label("Graph the AUC") groupbox			///
	onclickon(script graph_on)					///
	onclickoff(script graph_off)
  RADIO    rb_data         _indent   _ms       _cwd3_3   .,		///
	label("Data only") option("NONE") first				///
	onclickon(main.en_exp.disable)
  DEFINE holdx @x
  RADIO    rb_line         @         _ss       @         .,		///
	label("Line extension") option(line)				///
	onclickon(main.en_exp.disable)
  RADIO    rb_log          @         _ss       @         .,		///
	label("Log extension") option(log)				///
	onclickon(main.en_exp.disable)
  RADIO    rb_exp          @         _ss       @         .,		///
	label("Exponential extension out to point:")  last		///
	onclickon(gaction main.en_exp.enable) option(NONE)
  EDIT     en_exp          _indent2  _ss       _en7wd    .,		///
	option(exp) numonly label("Exponential extension out to point")
END

SCRIPT graph_on
BEGIN
	main.rb_data.enable
	main.rb_line.enable
	main.rb_log.enable
	main.rb_exp.enable

	script p1_opts_enable
	script addplot_enable
	script gr_twoway_enable
END

SCRIPT graph_off
BEGIN
	main.rb_data.disable
	main.rb_line.disable
	main.rb_log.disable
	main.rb_exp.disable
	main.en_exp.disable

	script p1_opts_disable
	script addplot_disable
	script gr_twoway_disable
END

INCLUDE byifin
INCLUDE p1_opts
INCLUDE addplot
INCLUDE gr_twoway

SCRIPT PREINIT
BEGIN
	script p1_opts_createAsNoRange
	script p1_opts_setDefaultConnected
END

SCRIPT POSTINIT
BEGIN
	p1_opts.settabtitle "AUC plot"
END

PROGRAM command
BEGIN
        INCLUDE _by_pr
	put "pkexamine "
	varlist main.vn_vart
	varlist main.vn_varc
	INCLUDE _ifin_pr
	beginoptions
	        option main.ck_trap
		if main.ck_fit {
			optionarg main.sp_fit
		}
		option main.ck_graph
		if main.ck_graph {

			option radio(main rb_data rb_line rb_log rb_exp)
			if main.rb_exp{
				require main.en_exp
				optionarg main.en_exp
			}
			put " " /program p1_opts_output
			put " " /program addplot_output
			put " " /program gr_twoway_output
		}
	endoptions
END

/*
	pkshape

	VERSION 1.0.0  21dec2002

*/

VERSION 8.0

INCLUDE _std_large
INCLUDE _ht320
INCLUDE header

HELP hlp1, view("help pkshape")
RESET res1

DIALOG main, label("pkshape - Reshapes pk data for use with anova, pkcross, pkequiv") /*
		*/ tabtitle("Main")
BEGIN
  /*--+----1----+----2----+----3----+----4----+----5----+----6----+----7----+*/ 
  TEXT     tx_vari         _lft      _top      _cwd3_1   .,		/*
		*/label("Identification variable:")
  TEXT     tx_vars         _lft3_2   @         _cwd3_1   .,             /*
  		*/label("Sequence variable:")
  TEXT     tx_varp         _lft3_3   @         @         .,		/*
  		*/label("First period variable:")
  VARNAME  vn_vari         _lft      _ss       _vnwd     .,             /*
  		*/label("Identification variable")
  VARNAME  vn_vars         _lft3_2   @         @         .,		/*
  		*/label("Sequence variable")
  VARNAME  vn_varp         _lft3_3   @         @         .,		/*
  		*/label("First period variable")
  TEXT     tx_varp2        _lft      _ms       _iwd      .,		/*
  		*/label("Remaining period variables:")			/*
  		*/
  VARLIST  vl_varp2        @         _ss       _iwd      .,		/*
  		*/label("Remaining period variables")
  GROUPBOX gb_opt          _lft      _ls       _iwd      _ht17,		/*
  		*/label("Options")
  CHECKBOX ck_order        _indent   _ss       _ibwd     .,		/*
*/label(`"Treatments applied in this order: (example: "abc cba bac")"')	/*
  		*/clickon(gaction main.ed_order.enable)			/*
		*/clickoff(gaction main.ed_order.disable)
  DEFINE _x @x
  EDIT     ed_order        _indent2  _ss       _inwd     .,		/*
  		*/label("Treatments applied in order")			/*
  		*/option("order")
  TEXT     tx_nvars        _x        _ls       _ibwd     .,		/*
  		*/label("Customize names for new variables:")
  CHECKBOX ck_out          _x        _ms       _cwd3_2   .,		/*
  		*/label("Outcome variable:")				/*
  		*/clickon(gaction main.ed_out.enable)			/*
		*/clickoff(gaction main.ed_out.disable)
  DEFINE _y @y
  EDIT     ed_out          _indent2  _ss       _vnwd     .,		/*
		*/max(32)						/*
  		*/label("Outcome variable")				/*
  		*/option("outcome")
  CHECKBOX ck_seq          _x        _ls       _cwd3_2   .,		/*
  		*/label("Sequence variable:")				/*
  		*/clickon(gaction main.ed_seq.enable)			/*
		*/clickoff(gaction main.ed_seq.disable)
  EDIT     ed_seq          _indent2  _ss       _vnwd     .,		/*
		*/max(32)						/*
  		*/option("sequence") label("Sequence variable")
  CHECKBOX ck_treat        _ilft3_2  _y        _cwd3_2   .,		/*
  		*/label("Treatment variable:")				/*
  		*/clickon(gaction main.ed_treat.enable)			/*
		*/clickoff(gaction main.ed_treat.disable)
  EDIT     ed_treat        _indent2  _ss       _vnwd     .,		/*
		*/max(32)						/*
  		*/label("Treatment variable")				/*
  		*/option("treatment")
  CHECKBOX ck_per          _ilft3_2  _ls       _cwd3_2   .,		/*
  		*/label("Period variable:")				/*
  		*/clickon(gaction main.ed_per.enable)			/*
		*/clickoff(gaction main.ed_per.disable)
  EDIT     ed_per          _indent2  _ss       _vnwd     .,		/*
		*/max(32)						/*
  		*/option("period") label("Period variable")
  CHECKBOX ck_carry        _ilft3_3  _y        _cwd3_2   .,		/*
  		*/label("Carryover variable:")				/*
  		*/clickon(gaction main.ed_carry.enable)			/*
		*/clickoff(gaction main.ed_carry.disable)
  EDIT     ed_carry        _indent2  _ss       _vnwd     .,		/*
		*/max(32)						/*
  		*/option("carryover") label("Carryover variable")
END

PROGRAM command
BEGIN
	put "pkshape "
	varlist main.vn_vari
	varlist main.vn_vars
	varlist main.vn_varp
	varlist main.vl_varp2
	beginoptions
		if main.ck_order {
			require main.ed_order
			optionarg main.ed_order
		}
		if main.ck_out {
			require main.ed_out
			optionarg main.ed_out
		}
		if main.ck_treat {
			require main.ed_treat
			optionarg main.ed_treat
		}
		if main.ck_carry {
			require main.ed_carry
			optionarg main.ed_carry
		}
		if main.ck_seq {
			require main.ed_seq
			optionarg main.ed_seq
		}
		if main.ck_per {
			require main.ed_per
			optionarg main.ed_per
		}
	endoptions
END


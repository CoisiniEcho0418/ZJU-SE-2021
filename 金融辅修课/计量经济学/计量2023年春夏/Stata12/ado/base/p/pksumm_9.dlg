/*
	pksumm_9

!*  VERSION 1.1.3  16aug2006 (deprecated)

*/

VERSION 9.0

INCLUDE graph_header
INCLUDE header

HELP hlp1, view("help pksumm")
RESET res1

DIALOG main, label("pksumm - Summarize pharmacokinetic data") tabtitle("Main")
BEGIN
  TEXT     tx_vari         _lft      _top      _cwd4_1   .,		/*
		*/label("Identifier variable:")
  TEXT     tx_vart         _lft4_2   @         @         .,		/*
  		*/label("Time variable:")				/*
  		*/
  TEXT     tx_varc         _lft4_3   @         @         .,		/*
  		*/label("Concentration variable:")			/*
  		*/
  VARNAME  vn_vari         _lft      _ss       _vnwd     .,             /*
  		*/label("Identifier variable")				/*
  		*/
  VARNAME  vn_vart         _lft4_2   @         @         .,		/*
  		*/label("Time variable")
  VARNAME  vn_varc         _lft4_3   @         @         .,		/*
  		*/label("Concentration variable")			/*
  		*/
  GROUPBOX gb_opt          _lft      _xls      _iwd      _ht17h,        /*
  		*/label("Options")
  CHECKBOX ck_trap         _indent   _ss       _cwd4_1   .,		/*
  		*/ label("Use trapezoidal rule")			/*
  		*/ option("trapezoid")
DEFINE _x @x
  CHECKBOX ck_fit          _lft4_2   @         200       .,		/*
		*/label("Points to use to extend AUC:")			/*
  		*/clickon(gaction main.sp_fit.enable)			/*
		*/clickoff(gaction main.sp_fit.disable)
  SPINNER  sp_fit          _indent2  _ss       _spwd     .,		/*
  		*/option("fit")						/*
  		*/min(3) max(10000000) default(3)			/*
		*/label("Points to use to extend AUC")
  CHECKBOX ck_notchk       _x        _ls       380       .,		/*
*/label("Do not check that the follow-up time for all subjects is the same")/*
		*/ option("notimechk")
  CHECKBOX ck_nodots       @         _ms       @         .,		/*
  		*/ option("nodots")					/*
		*/ label("Suppress the dots during calculation")
  CHECKBOX ck_graph        @         _ms       _ibwd     _ht4h,		/*
  		*/groupbox						/*
  		*/ label("Graph the distribution of the statistic specified")/*
		*/ option("graph")					/*
		*/ clickon("program graph_on")				/*
		*/ clickoff("script graph_off")
  CHECKBOX ck_stat         _indent   _ms       370       .,		/*
*/ label("Graph this statistic (see -help pksumm- for more information)")/*
		*/clickon(gaction main.cb_stat.enable)			/*
		*/clickoff(gaction main.cb_stat.disable)		/*
		*/
  COMBOBOX cb_stat         _indent2  _ss       _vnwd     .,		/*
  		*/dropdownlist						/*
		*/contents("measure")					/*
		*/option("stat")
END

INCLUDE ifin

PROGRAM graph_on
BEGIN
	call main.ck_stat.enable
	if main.ck_stat {
		call main.cb_stat.enable
	}
	call script ghistogram_enable
	call script gdensity_enable
	call script gr_twoway_enable
END

SCRIPT graph_off
BEGIN
	main.ck_stat.disable
	main.cb_stat.disable
	script ghistogram_disable
	script gdensity_disable
	script gr_twoway_disable
END

LIST measure
BEGIN
	auc
	aucline
	aucexp
	auclog
	half
	ke
	cmax
	tmax
	tomc
END

INCLUDE ghistogram
INCLUDE gdensity
INCLUDE gr_twoway

SCRIPT POSTINIT
BEGIN
	script gdensity_allow_kDensity
	script ghistogram_createAsOptionTab
END

PROGRAM command
BEGIN
	put "pksumm "
	varlist main.vn_vari
	varlist main.vn_vart
	varlist main.vn_varc
	INCLUDE _ifin_pr
	beginoptions
		if main.ck_fit {
			optionarg main.sp_fit
		}
	        option main.ck_trap
		option main.ck_notchk
		option main.ck_nodots
		option main.ck_graph
		if main.ck_graph {
			if main.ck_stat {
				require main.cb_stat
				optionarg main.cb_stat
			}
		}

		put " " /program ghistogram_options_output
		put " " /program gdensity_output
		put " " /program gr_twoway_output
	endoptions
END
/* end */

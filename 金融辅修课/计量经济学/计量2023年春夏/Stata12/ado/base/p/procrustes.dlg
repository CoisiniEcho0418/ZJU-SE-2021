/*
	procrustes

*!  VERSION 1.0.0  25feb2005

*/

VERSION 9.0

INCLUDE _std_large
INCLUDE header
HELP hlp1, view("help procrustes")
RESET res1

DIALOG main, tabtitle("Model") label("procrustes - Procrustes transformation")
BEGIN
  TEXT     tx_varlist_y		_lft	_top	_vlwd	.,		///
	label("Target variables:")
  VARLIST  vl_varlist_y		@	_ss	@	.,		///
	label("Target variables")
  TEXT     tx_varlist_x		@	_ls	@	.,		///
	label("Source variables:")
  VARLIST  vl_varlist_x		@	_ss	@	.,		///
	label("Source variables")

  GROUPBOX gb_trans		_lft	+35	_iwd	_ht6,		///
	label("Transformation method")
  RADIO	rb_default		_ilft	_ss	_ibwd	.,		///
	first label("Orthogonal rotation and reflection transformation (default)") 						///
	option(NONE)
  RADIO	rb_oblique		@	_ss	@	., 		///
	label("Oblique rotation transformation")			///
	option(transform(oblique))
  RADIO	rb_unrestrict		@	_ss	@ 	.,		///
	last label("Unrestricted transformation")			///
	option(transform(unrestricted))					///
	onclickon(main.ck_norho.disable)				///
	onclickoff(main.ck_norho.enable)
  CHECKBOX ck_noconstant	_lft	+45	_iwd	.,		///
	label("Suppress the constants")					///
	option(noconstant)
  CHECKBOX ck_norho		@	_ms	@	.,		///
	label("Suppress the dilation factor, i.e., set rho=1")		///
	option(norho)
  CHECKBOX ck_force		@	_ms	@	.,		///
	option(force)							///
	label("Allow overlap and duplicates in target and source lists (advanced)")
END

INCLUDE byifin
INCLUDE weights_fa

DIALOG rpt, tabtitle(Reporting)
BEGIN
  CHECKBOX ck_nofit		_lft	_top	_iwd	.,		///
	label("Suppress table of fit statistics by target variable")	///
	option(nofit)
END

PROGRAM vly_output
BEGIN
	varlist main.vl_varlist_y
END

PROGRAM vlx_output
BEGIN
	varlist main.vl_varlist_x
END

PROGRAM command
BEGIN
	put /program by_output " "
	put "procrustes ("
	put /program vly_output
	put ") ("
	put /program vlx_output
	put ") "
	INCLUDE _ifin_pr
 	INCLUDE _weights_pr
	beginoptions
		option radio(main rb_default rb_oblique rb_unrestrict)
		option main.ck_noconstant
		option main.ck_norho
		option main.ck_force
		option rpt.ck_nofit
        endoptions
END

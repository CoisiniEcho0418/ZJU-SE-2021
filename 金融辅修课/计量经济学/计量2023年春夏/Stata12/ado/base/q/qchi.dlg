/*
	qchi

*!  VERSION 2.0.0  27jun2006

*/

VERSION 10

INCLUDE _std_xlarge  // _std_xlarge is minimum size due to number of tabs (MAC)
DEFINE _dlght	250
INCLUDE header

HELP hlp1, view("help qchi")
RESET res1

DIALOG main, tabtitle("Main")					///
	label("qchi - Quantiles of chi-squared distribution plot")	
BEGIN

  TEXT     tx_var	_lft	_top	_iwd	.,		///
	label("Variable:")
  VARNAME  vn_var	@	_ss	_vnwd	.,		///
	label("Variable")

  GROUPBOX gb_opts	@	+35	_iwd	_ht6,		///
	label("Options")

  CHECKBOX ck_grid	_ilft	_ms	_inwd	.,		///
	option(grid)						///
	label("Add grid lines at percentiles 5, 10, 25, 50, 75, 90, and 95")

  SPINNER  sp_df	@	_ls	_spwd	.,		///
	label("Degrees of freedom")				///
	min(1) max(1000000) default(1)				///
	option(df)
  TEXT     tx_df	_spsep	@	200	.,		///
	label("Degrees of freedom")
END

SCRIPT POSTINIT
BEGIN
	gr_line_options_optionName.setstring "rlopts"
	gr_line_options.settabtitle "Reference line"
END

INCLUDE ifin

INCLUDE gr_plots
INCLUDE gr_line_options
INCLUDE gr_addplots
INCLUDE gr_yaxis
INCLUDE gr_xaxis
INCLUDE gr_titles
INCLUDE gr_legend
INCLUDE gr_overall

PROGRAM command
BEGIN
        put "qchi "
        varlist main.vn_var
        put " " /program ifin_output
	beginoptions
		option main.ck_grid
		optionarg /hidedefault main.sp_df
		put " " /program gr_plots_output
		put " " /program gr_line_options_output
		put " " /program gr_addplots_output
		put " " /program gr_yaxis_output
		put " " /program gr_xaxis_output
		put " " /program gr_titles_output
		put " " /program gr_legend_output
		put " " /program gr_overall_output
        endoptions
END

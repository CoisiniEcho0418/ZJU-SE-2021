/*
 roccomp 

*!  VERSION 2.0.1  07feb2007

*/

VERSION 10.0

INCLUDE _std_wide
DEFINE _dlght 310
INCLUDE header

HELP hlp1, view("help roccomp")
RESET res1

SCRIPT main_PREINIT
BEGIN
	create STRING lineOptionResults
	create STRING matrixResult
END

SCRIPT main_POSTINIT
BEGIN
	gr_line_options.settabtitle "Reference line"
	gr_line_options_optionName.setstring "rlopts"
	script gr_plots_setMultiView_on
	script gr_plots_setDefaultConnected
END

DIALOG main, tabtitle("Main")						///
	label("roccomp - Test equality of ROC areas")
BEGIN
  TEXT     tx_varr		_lft	_top	_vnwd	.,		///
	label("Reference variable:")
  TEXT     tx_varc		_lft4_2	@	_cwd4_1	.,		///
	label("Classification variable:")	
  TEXT     tx_varc2		_lft4_3	@	_cwd1	.,		///
	label("Additional classification variables:")
  
  VARNAME  vn_varr		_lft	_ss	_vnwd	., 		/// 
	label("Reference variable")
  VARNAME  vn_varc		_lft4_2	@	_vnwd	.,		/// 
	label("Classification variable:")	

  VARLIST  vl_varc2		_lft4_3	@	_cwd1	.,		///
	label("Reference variable")
  GROUPBOX gb_opt		_lft	_ls	_iwd	_ht21,		///
	label("Options")
  CHECKBOX ck_by		_indent	_ss	_cwd2	.,		///
	label("Split into groups by variable:")				///
	clickon(program main_by_on)					///
	clickoff(script main_by_off)
  DEFINE _x @x
  DEFINE _y @y
  VARNAME  vn_by		_indent2 _ss	_vnwd	.,		///
	option("by")							///
	label("Split into groups by variable")
  CHECKBOX ck_test		_lft2	_y	_cwd2	.,		///
	label("Use contrast matrix for comparing ROC areas")		///
	onclickon(script ck_test_on)					///
	onclickoff(script ck_test_off)
  COMBOBOX cb_test		_indent2 _ss	_vnwd	.,		///
	dropdownlist							///
	contents(matrix)						///
	option("test")							///
	label("Contrast matrix for comparing ROC areas")
  BUTTON   bu_matinput    	_vnsep	@	100	.,		///
	label("Input matrix...") onpush(program show_matrix_input)	///
	tooltip("Input matrix by hand")

  CHECKBOX ck_graph		_ilft	+35	_cwd1	.,		///
	clickon(program main_graph_on)					///
	clickoff("script main_graph_off")				///
	label("Graph the ROC curve")					///
	option("graph")
  CHECKBOX ck_noref		_indent	_ls	_cwd2	 .,		///
	label("Suppress plotting 45-degree reference line")		///
	clickon("script main_refline_options_disable")			///
	clickoff("program main_refline_options_enable")			///
	option("norefline")
  CHECKBOX ck_sep		@	_ss	@	.,		///	
	label("Place each ROC curve on its own graph ")			///
	option("separate")
  CHECKBOX ck_summ		@	_ss	@	.,		///
	label("Report the area under the ROC curve")			///
	option("summary")
  CHECKBOX ck_binorm		_x	_ls	_cwd2	.,		///	
	label("Estimate using binormal distribution")			///
	clickon(program main_binorm_enable)				///
	clickoff(script main_binorm_disable)				///
	option("binormal")						
  BUTTON bu_line_options	_lft4_3	@	160	.,		///
	label("Binormal fit line properties  ")				///
	onpush(script show_line_dialog)
  DEFINE _y _ls
  DEFINE _cx _sprb
  INCLUDE _sp_level
END

// Main scripts/programs
PROGRAM show_matrix_input
BEGIN
	call create CHILD matrix_input
	if ! _rc {
		call matrix_input.setExitString matrixResult
		call matrix_input.setExitAction "program getMatrixResult"
		call matrix_input.callthru "isChildContext.settrue"
	}
END
PROGRAM getMatrixResult
BEGIN
	call main.cb_test.repopulate
	call main.cb_test.setvalue class matrixResult.value
END

SCRIPT ck_test_on
BEGIN
	main.cb_test.enable
	main.bu_matinput.enable
END
SCRIPT ck_test_off
BEGIN
	main.cb_test.disable
	main.bu_matinput.disable
END

PROGRAM main_by_on
BEGIN
	call main.vn_by.enable
	if main.ck_graph {
		call main.ck_sep.enable
	}
END

SCRIPT main_by_off
BEGIN
	main.vn_by.disable
	main.ck_sep.disable
END

PROGRAM main_graph_on
BEGIN
	call main.ck_noref.enable
	if main.ck_noref {
		call script gr_line_options_disable
	}
	else {
		call script gr_line_options_enable
	}	
	if main.ck_binorm {
		call main.bu_line_options.enable
	}
	else {
		call main.bu_line_options.disable
	}	
	call main.ck_summ.enable
	if main.ck_by {
    		call main.ck_sep.enable
    	}
	call script gr_plots_enable
	call script gr_yaxis_enable
	call script gr_xaxis_enable
	call script gr_titles_enable
	call script gr_legend_enable
	call script gr_overall_enable
END

SCRIPT main_graph_off
BEGIN
	main.ck_noref.disable
	main.ck_sep.disable
	main.ck_summ.disable
	main.bu_line_options.disable
	
	script gr_line_options_disable
	script gr_plots_disable
	script gr_yaxis_disable
	script gr_xaxis_disable
	script gr_titles_disable
	script gr_legend_disable
	script gr_overall_disable
END

SCRIPT main_refline_options_disable
BEGIN
	script gr_line_options_disable
END

PROGRAM main_refline_options_enable
BEGIN
	if main.ck_graph {
		call script gr_line_options_enable
	}
END

PROGRAM main_binorm_enable
BEGIN
	if main.ck_graph {
		call main.bu_line_options.enable
	}
END
SCRIPT main_binorm_disable
BEGIN
	main.bu_line_options.disable
END

// Main Buttons
SCRIPT show_line_dialog
BEGIN
	create CHILD gr_plots AS lineOptions, allowsubmit
	lineOptions.setExitString lineOptionResults
	lineOptions.settitle "Binormal fit line properties"
	lineOptions.setExitAction "program checkLineOptionResults"
	lineOptions.setSubmitAction "program lineOptionSubmit"
	lineOptions.callthru "script gr_plots_setDefaultLine"
	lineOptions.callthru "script gr_plots_setMultiView_on"
	lineOptions.callthru `"gr_plots_outputCmdStub.setstring "line""'
END

PROGRAM checkLineOptionResults
BEGIN
	if lineOptionResults.iseq("") {
		call main.bu_line_options.setlabel "Binormal fit line properties  "
	}
	if ! lineOptionResults.iseq("") {
		call main.bu_line_options.setlabel "Binormal fit line properties *"
	}
END
PROGRAM lineOptionSubmit
BEGIN
	call program checkLineOptionResults
	call Submit
END

INCLUDE ifin
INCLUDE weights_f

INCLUDE gr_plots
INCLUDE gr_line_options
INCLUDE gr_yaxis
INCLUDE gr_xaxis
INCLUDE gr_titles
INCLUDE gr_legend
INCLUDE gr_overall

PROGRAM command
BEGIN
	put "roccomp "
	varlist main.vn_varr
	varlist main.vn_varc
	if main.vl_varc2 {
		varlist main.vl_varc2
	}
	put " " /program ifin_output	
	put " " /program weights_output	
	beginoptions
		if ! main.vl_varc2 & ! main.ck_by {
			stopbox stop					/// 
			`"On the Main tab, either use "Split into groups by variable""' /// 
			`"or fill in "Additional classification variables""'
		}
		if main.ck_by {
			require main.vn_by
			optionarg main.vn_by
		}
		if main.ck_test {
			require main.cb_test
			optionarg main.cb_test
		}
		option main.ck_graph
		option main.ck_binorm
		INCLUDE _level_main_pr
		if main.ck_graph {
			option main.ck_noref
			option main.ck_sep
			option main.ck_summ
			if lineOptionResults & ! H(main.bu_line_options) {
				put " " lineOptionResults " "
			}
			put " " /program gr_plots_output
			put " " /program gr_line_options_output
			put " " /program gr_yaxis_output
			put " " /program gr_xaxis_output
			put " " /program gr_titles_output
			put " " /program gr_legend_output
			put " " /program gr_overall_output
		}
	endoptions
END

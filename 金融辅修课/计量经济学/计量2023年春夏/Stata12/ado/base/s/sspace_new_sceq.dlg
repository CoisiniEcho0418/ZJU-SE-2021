/*
  sspace_new_sceq.dlg

*!  VERSION 1.0.2  30jun2009

  keyword:  eclass

*/

VERSION 11.0

INCLUDE _std_large
DEFINE _dlght 200
INCLUDE header

RESET res1

DIALOG main, tabtitle("Main")
BEGIN
  TEXT tx_statevar	_lft	_top	_iwd	.,			///
	label("Unobserved state name:")
  EDIT ed_statevar	@	_ss	_vnwd	.,			///
	label("Unobserved state name")

  TEXT tx_lstatevars	_lft	_ls	_iwd	.,			///
	label("Lagged state names: (optional)")
  EDIT ed_lstatevars	@	_ss	@	.,			///
	label("Lagged state names")

  TEXT tx_indepvars	_lft	_ls	_iwd	.,			///
	label("Independent variables: (optional)")
  VARLIST vl_indepvars	@	_ss	@	.,			///
	ts fv								///
	label("Independent variables")

  CHECKBOX ck_noerror	_lft	_ls	_cwd1	.,			///
	option(noerror)							///
	label("No error term in the equation")
  CHECKBOX ck_nocons	_lft2	@	_cwd1	.,			///
	option(noconstant)						///
	label("Suppress the constant term")
END

PROGRAM main_output
BEGIN
	put main.ed_statevar
	if main.ed_lstatevars {
		put " " main.ed_lstatevars
	}
	if main.vl_indepvars {
		put " " main.vl_indepvars
	}
	beginoptions
		put " state"
		option main.ck_noerror
		option main.ck_nocons
	endoptions

	if main.ck_noerror {
		call PARENT.callthru "main_sceq_has_noerror.settrue"
	}
END

PROGRAM command
BEGIN
	require main.ed_statevar
	put "(" /program main_output ")"
END

/*
  stcox

*!  VERSION 1.3.0  23aug2010

*/

VERSION 12.0

INCLUDE _std_large
DEFINE _dlght 335
INCLUDE header

HELP hlp1, view("help stcox")
RESET res1

SCRIPT PREINIT
BEGIN
	program parseMessage
	script se_createAsSvyInternalML
	script se_setNoAdjust_on
	create BOOLEAN stcox_robust
	program svy_check_title
END

PROGRAM parseMessage
BEGIN
	if __MESSAGE.contains("__MI__") {
		call script se_setMI_on
		call main.bu_stset.hide
	}
	else {
		call main.bu_mi_stset.hide
	}
END

SCRIPT POSTINIT
BEGIN
	program check_bytab
	program se_setFinalInitState
END

PROGRAM check_bytab
BEGIN
	if ! __MESSAGE.contains("__MI__") {
		call script sub_set_by_on
	}
END

SCRIPT svy_is_on
BEGIN
	main.ck_est.disable
	main.tx_shared.disable
	main.vn_shared.disable
	main.gb_tied.disable
	main.rb_breslow.disable
	main.rb_efron.disable
	main.rb_exactm.disable
	main.rb_exactp.disable
	script max_setDefaultNoLog
	tv.ck_tvc.disable
	script tvc_off

END
SCRIPT svy_is_off
BEGIN
	main.ck_est.enable
	main.tx_shared.enable
	main.vn_shared.enable
	main.gb_tied.enable
	main.rb_breslow.enable
	main.rb_efron.enable
	main.rb_exactm.enable
	main.rb_exactp.enable
	script max_setDefaultLog
	program check_tvc
END
PROGRAM check_tvc
BEGIN
	if (main.rb_exactm | main.rb_exactp) {
		call tv.ck_tvc.disable
		call program exact_on
	}
	else {
		call tv.ck_tvc.enable
		call script tvc_off
		call program exact_off
	}
END
PROGRAM svy_check_title
BEGIN
	if __MESSAGE.contains("-svy-") {
		call settitle "svy: stcox - Fit Cox proportional hazards model for survey data"
	}
END

DIALOG main, tabtitle("Model")						///
	title("stcox - Fit Cox proportional hazards model") // has svy title
BEGIN
  DEFINE _x _xsetbu
  DEFINE _y _top
  INCLUDE _bu_stset
  BUTTON   bu_mi_stset _x        _y        _setbuwd  .,			/*
		*/ onpush("view dialog mi_stset")			/*
		*/ label("Survival settings...")			/*
		*/ tooltip("Declare data to be survival-time data")

  TEXT     tx_iv     _lft      _topph    _comb4_3  .,			/*
		*/ label("Independent variables:")			/*
		*/
  VARLIST  vl_iv     _lft      _ss       _iwd      .,			/*
		*/ label("Independent variables") fv			/*
		*/
  CHECKBOX ck_est    @         _ls       @         .,			/*
		*/ label("Fit model without covariates")		/*
		*/ option("estimate")					/*
		*/ onclickon(main.vl_iv.disable)			/*
		*/ onclickoff(main.vl_iv.enable)
  GROUPBOX gb_output _lft      _ls       _cwd1      _ht15,		/*
		*/ label("Options")					/*
		*/
DEFINE y @y
  TEXT     tx_strata _ilft     _ss       _inwd     .,			/*
		*/ label("Strata ID variables:")			/*
		*/
  VARLIST  vn_strata @         _ss       @         .,			/*
		*/ label("Strata ID variables")				/*
		*/ option("strata")					/*
		*/
  TEXT     tx_shared @         _ls       _cwd3     .,			/*
		*/ label("Shared frailty ID variable:")			/*
		*/
  VARNAME  vn_shared @         _ss       _vnwd     .,			/*
		*/ label("Shared frailty ID variable")			/*
		*/ option("shared")					/*
		*/
  TEXT     tx_offset _ilft     _ls       _cwd3     .,			/*
		*/ label("Offset variable:")				/*
		*/
  VARNAME  vn_offset @         _ss       _vnwd     .,			/*
		*/ label("Offset variable")				/*
		*/ option("offset")					/*
		*/

  GROUPBOX gb_tied   _lft2     y         _cwd1     _ht15,		/*
		*/ label("Method to handle tied failures")		/*
		*/
  RADIO    rb_breslow _indent  _ls       _cwd3     .,			/*
		*/ label("Breslow")					/*
		*/ clickon("program exact_off")				/*
		*/ first						/*
		*/ option("NONE")					/*
		*/
  DEFINE y @y
  RADIO    rb_efron  @         _ss       @         .,			/*
		*/ label("Efron")					/*
		*/ clickon("program exact_off")				/*
		*/ option("efron")					/*
		*/
  RADIO    rb_exactm @         _ss       @         .,			/*
		*/ label("Exact marginal likelihood")			/*
		*/ clickon("program exact_on")				/*
		*/ option("exactm")					/*
		*/
  RADIO    rb_exactp @         _ss       @         .,			/*
		*/ label("Exact partial likelihood")			/*
		*/ clickon("program exact_on")				/*
		*/ last							/*
		*/ option("exactp")					/*
		*/
END

PROGRAM exact_on
BEGIN
	if stcox_robust {
		call script se_setClRobust_off
		call stcox_robust.setfalse
	}

	call script exacton
END

SCRIPT exacton
BEGIN
	tv.ck_tvc.disable
	script tvc_off
END

PROGRAM exact_off
BEGIN
	if !stcox_robust {
		call script se_setClRobust_on
		call stcox_robust.settrue
	}

	call script exactoff
	if tv.ck_tvc {
		call script tvc_on
	}
END

SCRIPT exactoff
BEGIN
	tv.ck_tvc.enable
END

DIALOG tv, tabtitle("Time varying")
BEGIN
  CHECKBOX ck_tvc    _lft      _top      _iwd      _ht8,		/*
		*/ label("Time-varying covariates")			/*
		*/ clickon("script tvc_on")				/*
		*/ clickoff("script tvc_off")				/*
		*/ groupbox						/*
		*/
  VARLIST  vl_tvc    _indent   _ms       _ibwd     .,			/*
		*/ label("Time-varying covariates")			/*
		*/ option("tvc")					/*
		*/
  TEXT     tx_texp   @         _ls       @         .,			/*
*/ label("Multiplier for time-varying covariates: (default: _t)")	/*
		*/
  EDIT     ed_texp   @         _ss       @         .,			/*
		*/ label("Multiplier for time-varying covariates")	/*
		*/ option("texp")					/*
		*/ default(_t)						/*
		*/
END

INCLUDE sub_by_ifin_over_subpop
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
END

DIALOG rpt, tabtitle("Reporting")
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y _top
  INCLUDE _sp_level

  CHECKBOX ck_nohr   _lft      _ls      _cwd0     .,			/*
		*/ label("Report coefficients, not hazard ratios")	/*
		*/ option("nohr")					/*
		*/
  CHECKBOX ck_noshow @         _ms       @         .,			/*
		*/ label("Do not show st setting information")		/*
		*/ option("noshow")					/*
		*/

  DEFINE _x _lft
  DEFINE _y _ms
  DEFINE _cx _iwd
  INCLUDE _noomitted

  DEFINE _x _lft
  DEFINE _y _ms
  DEFINE _cx _iwd
  INCLUDE _vsquish

  DEFINE _x _lft
  DEFINE _y _ms
  DEFINE _cx _iwd
  INCLUDE _factor_vars_reporting

  DEFINE _x _lft
  DEFINE _y _xxxls
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr

SCRIPT tvc_on
BEGIN
	tv.vl_tvc.enable
	tv.tx_texp.enable
	tv.ed_texp.enable
END

SCRIPT tvc_off
BEGIN
	tv.vl_tvc.disable
	tv.tx_texp.disable
	tv.ed_texp.disable
END

INCLUDE max_internal

PROGRAM command
BEGIN
	put /program by_output " "
	put /program se_prefix_output " "
	put "stcox "
	varlist [main.vl_iv]
	put " " /program ifin_output
	beginoptions
		optionarg main.vn_strata
		if tv.ck_tvc {
			require tv.vl_tvc
			optionarg tv.vl_tvc
			optionarg tv.ed_texp
		}
		put " " /program se_output
		optionarg main.vn_shared
		/* not doing frailty(gamma) because implied by shared() */

		option radio(main rb_breslow rb_efron rb_exactm rb_exactp)
		option main.ck_est
		optionarg main.vn_offset
		optionarg /hidedefault rpt.sp_level
		option rpt.ck_nohr
		option rpt.ck_noshow
		INCLUDE _noomitted_pr
		INCLUDE _vsquish_pr
		INCLUDE _factor_vars_reporting_pr
		put " " rpt_bu_fmtcoefResults
		put " " /program max_output
	endoptions
END

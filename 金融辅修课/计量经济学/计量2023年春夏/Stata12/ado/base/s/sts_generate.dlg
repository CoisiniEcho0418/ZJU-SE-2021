/*
   sts generate

*!  VERSION 2.0.2  17may2007

*/

VERSION 10.0

INCLUDE _std_large
INCLUDE _ht310
INCLUDE header

HELP hlp1, view("help sts")
RESET res1

DEFINE sep 160

DIALOG main,label("sts generate - Create survivor, hazard, and other variables")/*
                */ tabtitle("Main")
BEGIN
  DEFINE _x _xsetbu
  DEFINE _y _top
  INCLUDE _bu_stset

  TEXT     tx_gen         _lft      +10       _vnwd       .,            /*
                */ label("Generate:")
  DEFINE y @y

  FRAME    fr_gen         _lft      _ss       142         _ht23,        /*
                */ label("")

  EDIT     ed_gen         _indent   +8        _vnwd       .,            /*
                */ label("Generate")                                    /*
                */ max(32)

  TEXT     tx_equal       sep       @         10          .,            /*
		*/ label("=")

  TEXT     tx_produce     _lft3_2   y         _vnwd       .,            /*
                */ label("Producing:")

  FRAME    fr_produce     @         _ss       314         _ht23,        /*
                */ label("")

  RADIO    rb_s           _indent   +10       _inwd       ., first      /*
                */ label("Kaplan-Meier survivor function S(t)")         /*
                */ clickon("script enable_strata_adj_dis_level")	/*
		*/ option(s)

  RADIO    rb_se          @         _ss       @           .,            /*
		*/ label("Standard error of S(t)")                      /*
		*/ clickon("script disable_strata_adj_dis_level")	/*
		*/ option(se(s))

  RADIO    rb_h           @         _ss       @           .,            /*
            */ label("Hazard component deltaH_j = H(t_j) - H(t_(j-1))") /*
                */ clickon("script enable_strata_adj_dis_level")	/*
                */ option(h)

  RADIO    rb_se_lls      @         _ss       @           .,            /*
                */ label("Standard error of ln{-lnS(t)}")               /*
                */ clickon("script disable_strata_adj_dis_level")	/*
                */ option(se(lls))

  RADIO    rb_lbs         @         _ss       @           .,            /*
		*/ label("Lower bound of confidence interval for S(t)") /*
                */ clickon("script disable_strata_adj_en_level")	/*
                */ option(lb(s))

  RADIO    rb_ubs         @         _ss       @           .,            /*
                */ label("Upper bound of confidence interval for S(t) ")/*
                */ clickon("script disable_strata_adj_en_level")        /*
                */ option(ub(s))

  RADIO    rb_na          @         _ss       @           .,            /*
                */ label("Nelson-Aalen cumulative hazard function H(t)")/*
                */ clickon("script disable_strata_adj_dis_level")	/*
                */ option(na)

  RADIO    rb_se_na       @         _ss       @           .,            /*
		*/ label("Standard error of H(t)")                      /*
                */ clickon("script disable_strata_adj_dis_level")	/*
                */ option(se(na))

  RADIO    rb_lb_na       @         _ss       @           .,            /*
                */ label("Lower bound of confidence interval for H(t)") /*
                */ clickon("script disable_strata_adj_en_level")	/*
                */ option(lb(na))

  RADIO    rb_up_na       @         _ss       @           .,            /*
                */ label("Upper bound of confidence interval for H(t)") /*
		*/ clickon("script disable_strata_adj_en_level")	/*
                */ option(ub(na))

  RADIO    rb_n           @         _ss       @           .,            /*
		*/ label("Number at risk just prior to time t (n_t)")   /*
		*/ clickon("script disable_strata_adj_dis_level")	/*
                */ option(n)

  RADIO    rb_d           @         _ss       @           ., last       /*
                */ label("Number failing at time t (d_t)")              /*
		*/ clickon("script disable_strata_adj_dis_level")	/*
                */ option(d)
END

INCLUDE ifin

DIALOG opt, tabtitle("Options")
BEGIN

  GROUPBOX gb_by          _lft      _top      _iwd        _ht13,	///
                label("Calculation")

  CHECKBOX ck_byvar          _ilft     _ms       _inwd     .,		///
 		onclickon(program opt_by_on)				///
 		onclickoff(script opt_by_off)				///
 		label("Separate on different groups of specified variables: (by variables)")
  VARLIST  vl_byvar          @         _ss       @         .,		///
 		option(by)						///
 		label("Separate on different groups of specified variables")

  CHECKBOX ck_adjvar         _ilft     _ls       _ibwd     .,		///
  		onclickon(program opt_adjvar_on)			///
  		onclickoff(script opt_adjvar_off)			///
   		label("Adjust the estimates to zero values of adjustment variables:")
  VARLIST  vl_adjvar         @         _ss       @         .,		///
 		option(adjustfor)					///
 		label("Adjust the estimates to zero values of adjustment variables")

  CHECKBOX ck_strvar         @         _ls       @         .,		///
 		label("Stratify on grouping variables")

  DEFINE _x _lft
  DEFINE _cx _spr
  DEFINE _y _xxls
  INCLUDE _sp_level
END

PROGRAM opt_by_on
BEGIN
	call opt.vl_byvar.enable
	if opt.ck_adjvar {
		call opt.ck_strvar.enable
	}
END

SCRIPT opt_by_off
BEGIN
	opt.vl_byvar.disable
	opt.ck_strvar.disable
END

PROGRAM opt_adjvar_on
BEGIN
	call opt.vl_adjvar.enable
	if opt.ck_byvar {
		call opt.ck_strvar.enable
	}
END

SCRIPT opt_adjvar_off
BEGIN
	opt.vl_adjvar.disable
	opt.ck_strvar.disable
END

SCRIPT disable_strata_adj_en_level
BEGIN
	script strata_off
	script adjust_off
	script level_enable
END

SCRIPT disable_strata_adj_dis_level
BEGIN
	script strata_off
	script adjust_off
	script level_disable
END

SCRIPT enable_strata_adj_dis_level
BEGIN
	program strata_on
	program adjust_on
	script level_disable
END

SCRIPT strata_off
BEGIN
	opt.ck_strvar.disable
END

PROGRAM strata_on
BEGIN
	if opt.ck_adjvar & opt.ck_byvar {
		call gaction opt.ck_strvar.enable
	}
END

SCRIPT adjust_off
BEGIN
	opt.vl_adjvar.disable
	opt.ck_adjvar.disable
	opt.ck_strvar.disable
END

PROGRAM adjust_on
BEGIN
	call opt.ck_adjvar.enable
	if opt.ck_adjvar {
		call opt.vl_adjvar.enable
	}
	if opt.ck_byvar {
		call opt.ck_strvar.enable
	}
END

SCRIPT level_enable
BEGIN
	opt.sp_level.enable
	opt.tx_level.enable
END

SCRIPT level_disable
BEGIN
	opt.sp_level.disable
	opt.tx_level.disable
END

PROGRAM command
BEGIN
	require main.ed_gen
	if opt.ck_byvar {
		require opt.vl_byvar
	}
	if opt.ck_adjvar {
		require opt.vl_adjvar
	}
	put "sts generate "  main.ed_gen  " "  "  = "
	option radio(main rb_s rb_se rb_h rb_se_lls rb_lbs rb_ubs rb_na /*
				*/ rb_se_na rb_lb_na rb_up_na rb_n rb_d )
	INCLUDE _ifin_pr
	beginoptions
		if opt.ck_strvar.isenabled() & opt.ck_strvar {
			call opt.vl_byvar.setoption "strata"
		}
		else {
			call opt.vl_byvar.setoption "by"
		}
		optionarg opt.vl_byvar
		optionarg opt.vl_adjvar
		INCLUDE _level_opt_pr
	endoptions
END

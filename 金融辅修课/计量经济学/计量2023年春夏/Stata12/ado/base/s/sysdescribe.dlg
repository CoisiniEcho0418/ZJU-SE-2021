/*
  sysdescribe

*!  VERSION 1.0.0  13jan2005

*/

VERSION 9.0

INCLUDE _std_small
INCLUDE header

HELP hlp1, view("help sysdescribe")
RESET res1

DIALOG main, label("sysdescribe - Describe a shipped dataset")	///
	tabtitle("Main")
BEGIN
  GROUPBOX gb_opt	_lft	_top	_iwd	_ht4,
  RADIO    rb_file	_indent	_ss	_inwd	.,		///
	first label("Describe a specific shipped dataset")	///
	onclickon(script rb_file_on)
  RADIO    rb_dir	@	_ss	@	.,		///
	onclickon(script rb_dir_on) option(dir)			///
	last label("Show datasets in the adopath")
  
  TEXT     tx_file	_lft	+45	_iwd	_ht5,		///
	label("Dataset to describe:")
  EDIT     ed_file	@	_ss	_vnwd	.,		///
	label("Dataset to describe")
           
  CHECKBOX ck_all	@	-20	_ibwd	.,		///
	option(all)						///
	label("List all datasets, even those with an ( _ ) in their name")
END

SCRIPT rb_file_on
BEGIN
	main.ck_all.hide
	main.tx_file.show
	main.ed_file.show
END

SCRIPT rb_dir_on
BEGIN
	main.ck_all.show
	main.tx_file.hide
	main.ed_file.hide
END

PROGRAM trimfilename
BEGIN
	put main.ed_file
END

PROGRAM command
BEGIN
	put "sysdescribe "
	if main.rb_file {
		require main.ed_file
		put `"""'
		put /program trimfilename
		put `"""'
	}
	option main.rb_dir 
	beginoptions
		option main.ck_all
	endoptions
END


/*
  testanova_new_eqn.dlg

*!  VERSION 1.0.2  26jun2009

  keyword:  eclass

*/

VERSION 11.0

INCLUDE _std_large
DEFINE _dlght 120
INCLUDE header

RESET res1

PROGRAM POSTINIT_PROGRAM
BEGIN
	call create STRING cmd_sub
	call create BOOLEAN use_eqname

	call cmd_sub.setvalue e(cmd)

	if cmd_sub.iseq("manova") {
		call script use_depvar
		call use_eqname.setfalse
	}
	else {
		call script use_eqnames
		call use_eqname.settrue
	}
END

DIALOG main, tabtitle("Main")
BEGIN
  TEXT tx_eqn		_lft	_top	_cwd1	.,			///
	label("Equation: (required)")
  COMBOBOX cb_eqn1	@	_ss	_cwd1	.,			///
	dropdown							///
	contents(ereturn eqnames) 					///
	label("Equation")
  COMBOBOX cb_eqn2	@	@	@	.,			///
	dropdown							///
	contents(ereturn depvar) 					///
	label("Equation")
  TEXT tx_coef		@	_ls	_iwd	.,			///
	label("Test these coefficients: (leave blank for all)")
  VARLIST vl_coef	@	_ss	_iwd	.,			///
	label("Test these coefficients")
END

SCRIPT use_depvar
BEGIN
	main.cb_eqn1.hide
	main.cb_eqn2.show
END

SCRIPT use_eqnames
BEGIN
	main.cb_eqn1.show
	main.cb_eqn2.hide
END

PROGRAM command
BEGIN
	if use_eqname {
		require main.cb_eqn1
		put "([" main.cb_eqn1 "]"
	}
	else {
		require main.cb_eqn2
		put "([" main.cb_eqn2 "]"
	}

	if main.vl_coef {
		put ":" main.vl_coef
	}
	put ")"
END

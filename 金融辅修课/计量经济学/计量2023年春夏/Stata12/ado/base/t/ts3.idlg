/*
 ts3 -- include file for plot3 of tsline

*!  VERSION 1.1.2  01apr2005

*/

DIALOG p3, label("tsline - Time-Series line plots")  tabtitle("Plot 3")
BEGIN
  RADIO    rb_none     _lft        _top      60          .,		///
  		label("None")						///
  		first							///
  		onclickon(script p3_tsnone_on)
  RADIO    rb_tsline   +65         @         145         .,		///
  		label("Time-Series line plot")				///
  		onclickon(script p3_tsline_on)
  RADIO    rb_tsrline  +150        @         207         .,		///
  		label("Time-Series range plot with lines")		///
  		last							///
  		onclickon(script p3_tsrline_on)

  TEXT     tx_type     +212        @         _txwd       .,		///
                label("Recast to:") 					///
                right
  COMBOBOX cb_tstype   _txsep      @         _vnwd       .,		///
                label("Recast to") contents(graph_tstype)		///
                onselchangelist(p3_tsline_change) 			///
                option(recast)						///
                dropdownlist
  COMBOBOX cb_tsrtype  @           @         _vnwd       .,		///
                label("Recast to") contents(graph_tsrtype)		///
                onselchangelist(p3_tsrline_change) 			///
                option(recast)						///
                dropdownlist

  TEXT     tx_var      _lft        _xls      _yvarwd     .,		///
                label("Y:") right

  VARNAME  vn_var      _yvarsep    @         _vnwd       .,		///
                label("Y variable")

  TEXT     tx_var2     _ilft5_2    @         _yvarwd     .,		///
  		label("Y2:") right
  VARNAME  vn_var2     _yvarsep    @         _vnwd       .,		///
  		label("Y2 variable")


DEFINE holdx @x
  TEXT     tx_if       _ilft5_2    @         _yvarwd     .,		///
 		label("if:") right					///

  EXP      ex_if       _yvarsep    @         _gifwd      .,		///
                label("Create...")

  CHECKBOX ck_rtaxis   +313        @         _vnwd       .,		///
  		option(yaxis(2))					///
  		label("Plot on right")

  TEXT     tx_if2      _ilft5_3    @         _yvarwd     .,		///
 		label("if:") right					///

  EXP      ex_if2      _yvarsep    @         _gifwd      .,		///
                label("Create...")


/* BEGIN Line options */
  GROUPBOX gb_line     _lft        _ls       _gwd1       _ht4h,		///
  		label("Lines")
DEFINE opttop @y
  TEXT     tx_clcol    _igft       _ss       _txwd       .,		///
  		label("Color:") 					///
  		right
DEFINE holdy @y
  COMBOBOX cb_clcol    _txsep      @         _tcr2b4g    .,		///
  		option(lcolor)						///
  		dropdownlist						///
  		contents(colors) 					///
  		onselchangelist(p3_color_clcol)
  COLOR    cl_clcol    _tcr2b4gsep @         _colorwd    .,		///
  		option(lcolor)

  TEXT     tx_clpat    _igft       _ms       _txwd       .,		///
  		label("Pattern:") right
  COMBOBOX cb_clpat    _txsep      @         _txr2b4g    .,		///
  		dropdownlist 						///
  		contents(linepatterns)  				///
  		option(lpattern)

  TEXT     tx_clwidth  _gft4_2     holdy     _txwd       .,		///
  		label("Width:") right
  COMBOBOX cb_clwidth  _txsep      @         _txr2b4g    .,		///
  		dropdownlist						///
  		contents(linewidths) 					///
  		option(lwidth)

  TEXT     tx_conn     _gft4_2     _ms       _txwd       .,		///
  		label("Type:") right
  COMBOBOX cb_conn     _txsep      @         _txr2b4g    .,		///
  		option(connect)						///
  		dropdownlist 						///
  		contents(connecttypes)
/* END Line options */

/* BEGIN Scatter options */
  GROUPBOX gb_marker   _lft        +35       _gwd1       _ht12h,	///
  		label("Markers")
DEFINE ymidopts @y
  TEXT     tx_msym     _igft       _ss       _txwd       .,		///
  		label("Symbol:") right

  COMBOBOX cb_msym     _txsep      @         _txr2b4g    .,		///
  		option(msymbol)						///
  		dropdownlist						///
  		contents(symbols)

  TEXT     tx_mcol     _gft4_2     @         _txwd       .,		///
  		label("Color:") right
  COMBOBOX cb_mcol     _txsep      @         _tcr2b4g    .,		///
  		option(mcolor)						///
  		dropdownlist						///
  		contents(colors) 					///
		onselchangelist(p3_color_mcol)
  COLOR    cl_mcol     _tcr2b4gsep @         _colorwd    .,		///
  		option(mcolor)

  TEXT     tx_msize    _igft       _ms       _txwd       .,		///
  		label("Size:") right
  COMBOBOX cb_msize    _txsep      @         _txr2b4g    .,		///
  		option(msize)						///
  		dropdownlist 						///
  		contents(symbolsizes)


  CHECKBOX ck_mlab     _igft       _ls       _cwd1gb     _ht4h,		///
  		label("Marker labels") groupbox				///
  		onclickon(script p3_mlabel_enable)			///
		onclickoff(script p3_mlabel_disable)
  TEXT     tx_mlab     _iigft      _ss       _txswd      .,		///
  		label("Variable:") right
DEFINE holdy @y
  VARLIST  vl_mlab     _txssep     @         _txr2b4g    .,		///
  		label("Marker labels variable") option(mlabel)
  TEXT     tx_mlsiz    _iigft      _ms       _txswd      .,		///
  		label("Size:") right
  COMBOBOX cb_mlsiz    _txssep     @         _txr2b4g    .,		///
  		option(mlabsize) 					///
  		dropdownlist						///
  		contents(textsizes)

  TEXT     tx_mlcol    _gft4_2     holdy     _txswd      .,		///
  		label("Color:") right
  COMBOBOX cb_mlcol    _txssep     @         _tcr2b4g    .,		///
  		option(mlabcolor)					///
  		dropdownlist						///
  		contents(colors) 					///
  		onselchangelist(p3_color_mlcol)

  COLOR    cl_mlcol    _tcr2b4gsep @         _colorwd    .,		///
  		option(mlabcolor)

  TEXT     tx_mlpos    _gft4_2     _ms       _txswd      .,		///
  		label("Position:") right
  COMBOBOX cb_mlpos    _txssep     @         _txr2b4g    .,		///
  		option(mlabposition) 					///
  		dropdownlist						///
  		contents(clockpos)

/* END Scatter options */

/* BEGIN Orientation options */
  GROUPBOX gb_orient   _lft2g      opttop    _gwd1       _ht4h,		///
		label("Orientation")
  RADIO    rb_vertical _igft4_3    +22       _cwd4_gi    .,		///
   		label("Vertical") 					///
   		first 							///
   		option(vertical)
  DEFINE topy @y
  RADIO    rb_horizontal @         _ss       @           .,		///
   		label("Horizontal") 					///
   		last 							///
   		option(horizontal)
/* END Orientation options */

/* BEGIN Area, Bar, Spike, Dropline options */
  GROUPBOX gb_common   _lft2g      ymidopts  _gwd1       _ht7,		///
		label("Area")
  TEXT     tx_blcol    _igft4_3    _ss       _txwd       .,		///
  		label("Line color:") 					///
  		right
DEFINE holdy @y
  COMBOBOX cb_blcol    _txsep      @         _tcr2b4g    .,		///
  		contents(colors) 					///
  		option(lcolor) 					///
  		dropdownlist						///
		onselchangelist(p3_color_blcol)
  COLOR    cl_blcol    _tcr2b4gsep @         _colorwd    .,		///
  		option(lcolor)

  TEXT     tx_blwid    _igft4_3    _ms       _txwd       .,		///
  		label("Line width:") right
  COMBOBOX cb_blwid    _txsep      @         _txr2b4g    .,		///
  		contents(linewidths) 					///
  		option(lwidth)						///
  		dropdownlist

  TEXT     tx_base     _igft4_3    _ms       _txwd       .,		///
  		label("Base:") right
  EDIT     en_base     _txsep      @         _txr2b4g    .,		///
  		label("Base") 						///
  		default(0) 						///
  		option(base) 						///
  		numonly

// bfcol is used with area and bar
  TEXT     tx_bfcol    _igft4_4    holdy     _txwd       .,		///
  		label("Fill color:") right
  COMBOBOX cb_bfcol    _txsep      @         _tcr2b4g    .,		///
  		contents(colors) 					///
  		option(fcolor)						///
  		dropdownlist 						///
  		onselchangelist(p3_color_bfcol)
  COLOR    cl_bfcol    _tcr2b4gsep @         _colorwd    .,		///
  		option(fcolor)

// blpat is used with area
  TEXT     tx_blpat    _igft4_4    _ms       _txwd       .,		///
  		label("Ln. pattern:") right
  COMBOBOX cb_blpat    _txsep      @         _txr2b4g    .,		///
  		contents(linepatterns) 					///
  		dropdownlist 						///
  		option(lpattern)

// blpat2 is used with spike and dropline
  TEXT     tx_blpat2   _igft4_4    holdy     _txwd       .,		///
  		label("Ln. pattern:") right
  COMBOBOX cb_blpat2   _txsep      @         _txr2b4g    .,		///
  		contents(linepatterns) 					///
  		dropdownlist 						///
  		option(lpattern)

// barwidth is used with bar
  TEXT     tx_barwidth _igft4_4    _ms       _txwd       .,		///
  		label("Bar width:") right
  EDIT     en_barwidth _txsep      @         _txr2b4g    .,		///
  		label("Bar width")					///
  		default(1) 						///
  		option(barwidth) 					///
  		numonly

// capsize is used with tsrline rcap only
  TEXT     tx_capsize  _igft4_4    @         _txwd       .,		///
  		label("Cap size:") right
  COMBOBOX cb_capsize  _txsep      @         _txr2b4g    .,		///
  		option(msize)						///
  		dropdownlist 						///
  		contents(symbolsizes)

/* END Area, Bar, Spike, Dropline options */
END

LIST p3_color_mcol
BEGIN
	p3.cl_mcol.hide
	p3.cl_mcol.show
	p3.cl_mcol.hide
END

LIST p3_color_mlcol
BEGIN
	p3.cl_mlcol.hide
	p3.cl_mlcol.show
	p3.cl_mlcol.hide
END

LIST p3_color_clcol
BEGIN
	p3.cl_clcol.hide
	p3.cl_clcol.show
	p3.cl_clcol.hide
END

LIST p3_color_bfcol
BEGIN
	p3.cl_bfcol.hide
	p3.cl_bfcol.show
	p3.cl_bfcol.hide
END

LIST p3_color_blcol
BEGIN
	p3.cl_blcol.hide
	p3.cl_blcol.show
	p3.cl_blcol.hide
END


LIST p3_tsline_change
BEGIN
	program p3_do_line
	program p3_do_scatter
	program p3_do_connected
	program p3_do_area
	program p3_do_bar
	program p3_do_spike
	program p3_do_dropline
END

LIST p3_tsrline_change
BEGIN
	program p3_do_rline
	program p3_do_rscatter
	program p3_do_rconnected
	program p3_do_area
	program p3_do_bar
	program p3_do_spike
	program p3_do_rcap
	program p3_do_rcapsym
END

SCRIPT p3_tsnone_on
BEGIN
	//set the dialog to the initial state (ie tsline)
	script p3_tsline_on

	script p3_line_opts_off
	script p3_scatter_opts_off
	script p3_orient_opts_off
	script p3_common_area_opts_off
	script p3_area_opts_off
	script p3_bar_opts_off
	script p3_spike_drop_opts_off
	script p3_rcap_opts_off

	p3.tx_type.hide
	p3.cb_tstype.hide
	p3.ck_rtaxis.hide

	// grey out everything else
	p3.tx_var.disable
	p3.vn_var.disable
	p3.tx_if.disable
	p3.ex_if.disable
END

SCRIPT p3_tsline_on
BEGIN
	// enable components that were greyed in SCRIPT p3_tsnone_on
	p3.tx_var.enable
	p3.vn_var.enable
	p3.tx_if.enable
	p3.ex_if.enable

	p3.cb_tsrtype.hide
	p3.tx_var2.hide
	p3.vn_var2.hide
	p3.tx_if2.hide
	p3.ex_if2.hide
	p3.tx_base.enable
	p3.en_base.enable

	p3.tx_type.show
	p3.cb_tstype.show
	p3.cb_tstype.withvalue p3.cb_tstype.setvalue "@"
	p3.tx_if.show
	p3.ex_if.show
	p3.ck_rtaxis.show
	p3.ck_rtaxis.setposition 500 .
END

SCRIPT p3_tsrline_on
BEGIN
	// enable components that were greyed in SCRIPT p3_tsnone_on
	p3.tx_var.enable
	p3.vn_var.enable
	p3.tx_if.enable
	p3.ex_if.enable

	p3.cb_tstype.hide
	p3.tx_if.hide
	p3.ex_if.hide
	p3.ck_rtaxis.show
	p3.ck_rtaxis.setposition 640 .

	p3.tx_type.show
	p3.cb_tsrtype.show
	p3.cb_tsrtype.withvalue p3.cb_tsrtype.setvalue "@"
	p3.tx_var2.show
	p3.vn_var2.show
	p3.tx_if2.show
	p3.ex_if2.show

	// base does not apply to ranges
	p3.tx_base.disable
	p3.en_base.disable
END

SCRIPT p3_line_opts_on
BEGIN
	p3.gb_line.enable
	p3.tx_clcol.enable
	p3.cb_clcol.enable
	p3.cl_clcol.enable
	p3.tx_clpat.enable
	p3.cb_clpat.enable
	p3.tx_clwidth.enable
	p3.cb_clwidth.enable
	p3.tx_conn.enable
	p3.cb_conn.enable
END

SCRIPT p3_line_opts_off
BEGIN
	p3.gb_line.disable
	p3.tx_clcol.disable
	p3.cb_clcol.disable
	p3.cl_clcol.disable
	p3.tx_clpat.disable
	p3.cb_clpat.disable
	p3.tx_clwidth.disable
	p3.cb_clwidth.disable
	p3.tx_conn.disable
	p3.cb_conn.disable
END

SCRIPT p3_mlabel_enable
BEGIN
	p3.tx_mlab.enable
	p3.vl_mlab.enable
	p3.tx_mlsiz.enable
	p3.cb_mlsiz.enable
	p3.tx_mlcol.enable
	p3.cb_mlcol.enable
	p3.cl_mlcol.enable
	p3.tx_mlpos.enable
	p3.cb_mlpos.enable
END

PROGRAM p3_scatter_opts_on
BEGIN
	call p3.gb_marker.enable
	call p3.tx_msym.enable
	call p3.cb_msym.enable
	call p3.tx_mcol.enable
	call p3.cb_mcol.enable
	call p3.cl_mcol.enable
	call p3.tx_msize.enable
	call p3.cb_msize.enable
	call p3.ck_mlab.enable
	if p3.ck_mlab {
		call script p3_mlabel_enable
	}
END


SCRIPT p3_mlabel_disable
BEGIN
	p3.tx_mlab.disable
	p3.vl_mlab.disable
	p3.tx_mlsiz.disable
	p3.cb_mlsiz.disable
	p3.tx_mlcol.disable
	p3.cb_mlcol.disable
	p3.cl_mlcol.disable
	p3.tx_mlpos.disable
	p3.cb_mlpos.disable
END

SCRIPT p3_scatter_opts_off
BEGIN
	p3.gb_marker.disable
	p3.tx_msym.disable
	p3.cb_msym.disable
	p3.tx_mcol.disable
	p3.cb_mcol.disable
	p3.cl_mcol.disable
	p3.tx_msize.disable
	p3.cb_msize.disable
	p3.ck_mlab.disable
	script p3_mlabel_disable

END

SCRIPT p3_orient_opts_on
BEGIN
	p3.gb_orient.show
	p3.rb_vertical.show
	p3.rb_horizontal.show
END

SCRIPT p3_orient_opts_off
BEGIN
	p3.gb_orient.hide
	p3.rb_vertical.hide
	p3.rb_horizontal.hide
END

/* Common opts for area, bar, spike, and dropline */
SCRIPT p3_common_area_opts_on
BEGIN
	p3.gb_common.show
	p3.tx_blcol.show
	p3.cb_blcol.show
	p3.cb_blcol.withvalue p3.cb_blcol.setvalue "@"
	p3.tx_blwid.show
	p3.cb_blwid.show
	p3.tx_base.show
	p3.en_base.show
END

/* Common opts for area, bar, spike, and dropline */
SCRIPT p3_common_area_opts_off
BEGIN
	p3.gb_common.hide
	p3.tx_blcol.hide
	p3.cb_blcol.hide
	p3.cl_blcol.hide
	p3.tx_blwid.hide
	p3.cb_blwid.hide
	p3.tx_base.hide
	p3.en_base.hide
END

SCRIPT p3_area_opts_on
BEGIN
	script p3_common_area_opts_on
	p3.tx_blpat.show
	p3.cb_blpat.show
	p3.tx_bfcol.show
	p3.cb_bfcol.show
	p3.cb_bfcol.withvalue p3.cb_bfcol.setvalue "@"
END

SCRIPT p3_area_opts_off
BEGIN
	script p3_common_area_opts_off
	p3.tx_blpat.hide
	p3.cb_blpat.hide
	p3.tx_bfcol.hide
	p3.cb_bfcol.hide
	p3.cl_bfcol.hide
END

SCRIPT p3_bar_opts_on
BEGIN
	script p3_common_area_opts_on
	p3.tx_bfcol.show
	p3.cb_bfcol.show
	p3.cb_bfcol.withvalue p3.cb_bfcol.setvalue "@"
	p3.tx_barwidth.show
	p3.en_barwidth.show
END

SCRIPT p3_bar_opts_off
BEGIN
	script p3_common_area_opts_off
	p3.tx_bfcol.hide
	p3.cb_bfcol.hide
	p3.cl_bfcol.hide
	p3.tx_barwidth.hide
	p3.en_barwidth.hide
END

SCRIPT p3_spike_drop_opts_on
BEGIN
	script p3_common_area_opts_on
	p3.tx_blpat2.show
	p3.cb_blpat2.show
END

SCRIPT p3_spike_drop_opts_off
BEGIN
	script p3_common_area_opts_off
	p3.tx_blpat2.hide
	p3.cb_blpat2.hide
END

SCRIPT p3_rcap_opts_on
BEGIN
	script p3_common_area_opts_on
	p3.tx_capsize.show
	p3.cb_capsize.show
END

SCRIPT p3_rcap_opts_off
BEGIN
	script p3_common_area_opts_off
	p3.tx_capsize.hide
	p3.cb_capsize.hide
END

PROGRAM p3_do_line
BEGIN
	// test hidden condition to prevent action from LIST on load
	if !H(p3.cb_tstype) {
		call script p3_scatter_opts_off
		call script p3_orient_opts_off
		call script p3_area_opts_off
		call script p3_bar_opts_off
		call script p3_spike_drop_opts_off
		call script p3_rcap_opts_off

		call script p3_line_opts_on
	}
END

PROGRAM p3_do_rline
BEGIN
	// test hidden condition to prevent action from LIST on load
	if !H(p3.cb_tsrtype) {
		call script p3_scatter_opts_off
		call script p3_area_opts_off
		call script p3_bar_opts_off
		call script p3_spike_drop_opts_off
		call script p3_rcap_opts_off

		call script p3_line_opts_on
		call script p3_orient_opts_on
	}
END

PROGRAM p3_do_scatter
BEGIN
	call script p3_line_opts_off
	call script p3_orient_opts_off
	call script p3_area_opts_off
	call script p3_bar_opts_off
	call script p3_spike_drop_opts_off
	call script p3_rcap_opts_off

	call program p3_scatter_opts_on
END

PROGRAM p3_do_rscatter
BEGIN
	call script p3_line_opts_off
	call script p3_area_opts_off
	call script p3_bar_opts_off
	call script p3_spike_drop_opts_off
	call script p3_rcap_opts_off

	call program p3_scatter_opts_on
	call script p3_orient_opts_on
END

PROGRAM p3_do_connected
BEGIN
	call script p3_orient_opts_off
	call script p3_area_opts_off
	call script p3_bar_opts_off
	call script p3_spike_drop_opts_off
	call script p3_rcap_opts_off

	call script p3_line_opts_on
	call program p3_scatter_opts_on
END

PROGRAM p3_do_rconnected
BEGIN
	call script p3_area_opts_off
	call script p3_bar_opts_off
	call script p3_spike_drop_opts_off
	call script p3_rcap_opts_off

	call script p3_line_opts_on
	call program p3_scatter_opts_on
	call script p3_orient_opts_on
END

PROGRAM p3_do_area
BEGIN
	call script p3_line_opts_off
	call script p3_scatter_opts_off
	call script p3_bar_opts_off
	call script p3_spike_drop_opts_off
	call script p3_rcap_opts_off

	call script p3_orient_opts_on
	call script p3_area_opts_on
	call p3.gb_common.setlabel "Area"
END


PROGRAM p3_do_bar
BEGIN
	call script p3_line_opts_off
	call script p3_scatter_opts_off
	call script p3_area_opts_off
	call script p3_spike_drop_opts_off

	call script p3_orient_opts_on
	call script p3_bar_opts_on
	call p3.gb_common.setlabel "Bars"
END

PROGRAM p3_do_spike
BEGIN
	call script p3_line_opts_off
	call script p3_scatter_opts_off
	call script p3_area_opts_off
	call script p3_bar_opts_off
	call script p3_rcap_opts_off

	call script p3_orient_opts_on
	call script p3_spike_drop_opts_on
	call p3.gb_common.setlabel "Spikes"
END

PROGRAM p3_do_rcap
BEGIN
	call script p3_line_opts_off
	call script p3_scatter_opts_off
	call script p3_area_opts_off
	call script p3_bar_opts_off

	call script p3_orient_opts_on
	call script p3_spike_drop_opts_on
	call p3.gb_common.setlabel "Spikes"
	call script p3_rcap_opts_on
END

PROGRAM p3_do_dropline
BEGIN
	call script p3_line_opts_off
	call script p3_area_opts_off
	call script p3_bar_opts_off
	call script p3_rcap_opts_off

	call program p3_scatter_opts_on
	call script p3_orient_opts_on
	call script p3_spike_drop_opts_on
	call p3.gb_common.setlabel "Droplines"
END

PROGRAM p3_do_rcapsym
BEGIN
	call script p3_line_opts_off
	call script p3_area_opts_off
	call script p3_bar_opts_off
	call script p3_rcap_opts_off

	call program p3_scatter_opts_on
	call script p3_orient_opts_on
	call script p3_spike_drop_opts_on
	call p3.gb_common.setlabel "Spikes"
END

PROGRAM p3_line_opts
BEGIN
	if !Hcc(p3.cb_clcol) {
		optionarg /hidedefault p3.cb_clcol
	}
	if Hcc(p3.cb_clcol) {
		optionarg /quoted p3.cl_clcol
	}
	optionarg p3.cb_clpat
	optionarg p3.cb_clwidth
	optionarg p3.cb_conn
END

PROGRAM p3_rline_opts
BEGIN
	// options and optionarg are not used here because tsrline
	// uses different options, therefore they are hard coded
	if !Hcc(p3.cb_clcol) & p3.cb_clcol {
		put " " "lcolor(" p3.cb_clcol ")"
	}
	if Hcc(p3.cb_clcol) {
		put " " `"lcolor(""' p3.cl_clcol `"")"'
	}
	if p3.cb_clpat {
		put " " "lpattern(" p3.cb_clpat ")"
	}
	if p3.cb_clwidth {
		put " " "lwidth(" p3.cb_clwidth ")"
	}
	optionarg p3.cb_conn
END

PROGRAM p3_scatter_opts
BEGIN
	optionarg p3.cb_msym
	if !Hcc(p3.cb_mcol) {
		optionarg /hidedefault p3.cb_mcol
	}
	if Hcc(p3.cb_mcol) {
		optionarg /quoted p3.cl_mcol
	}
	optionarg p3.cb_msize
	if p3.ck_mlab {
		require p3.vl_mlab
		optionarg p3.vl_mlab
		optionarg p3.cb_mlsiz
		if !Hcc(p3.cb_mlcol) {
			optionarg /hidedefault p3.cb_mlcol
		}
		if Hcc(p3.cb_mlcol) {
			optionarg /quoted p3.cl_mlcol
		}
		optionarg p3.cb_mlpos
	}
END


/* Common opts for area, bar, spike, and dropline */
PROGRAM p3_common_area_opts
BEGIN
	if !Hcc(p3.cb_blcol) {
		optionarg /hidedefault p3.cb_blcol
	}
	if Hcc(p3.cb_blcol) {
		optionarg /quoted p3.cl_blcol
	}
  	optionarg p3.cb_blwid
  	optionarg /hidedefault p3.en_base


	// bfcol is used with area and bar
	if !Hcc(p3.cb_bfcol) {
		optionarg /hidedefault p3.cb_bfcol
	}
	if Hcc(p3.cb_bfcol) {
		optionarg /quoted p3.cl_bfcol
	}

	// blpat is used with area
	optionarg p3.cb_blpat

	// blpat2 is used with spike and dropline
	optionarg p3.cb_blpat2

	// barwidth is used with bar
	optionarg /hidedefault p3.en_barwidth

	// capsize is used with tsrline rcap only
	optionarg /hidedefault p3.cb_capsize
END

PROGRAM p3_tsline_sub
BEGIN
	varlist p3.vn_var
	ifexp p3.ex_if
	beginoptions
		optionarg p3.cb_tstype
		put " " /program p3_line_opts
		put " " /program p3_scatter_opts
		option p3.rb_horizontal
		put " " /program p3_common_area_opts
		option p3.ck_rtaxis
	endoptions
END

PROGRAM p3_tsrline_sub
BEGIN
	varlist p3.vn_var
	varlist p3.vn_var2
	ifexp p3.ex_if2
	beginoptions
		optionarg p3.cb_tsrtype
		put " " /program p3_rline_opts
		put " " /program p3_scatter_opts
		option p3.rb_horizontal
		put " " /program p3_common_area_opts
		option p3.ck_rtaxis
	endoptions
END

PROGRAM p3_output
BEGIN
	if p3.rb_tsline {
		put "(tsline "
		put /program p3_tsline_sub
		put ") "
	}
	if p3.rb_tsrline {
		put "(tsrline "
		put /program p3_tsrline_sub
		put ") "
	}
END

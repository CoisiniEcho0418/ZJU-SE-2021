/*
    varnorm.dlg

*!  VERSION 1.1.0  07feb2007

*/

VERSION 8.0

INCLUDE _std_large
INCLUDE _ht280
INCLUDE header

HELP hlp1, view("help varnorm")
RESET res1

DIALOG main, tabtitle("Main")					///
	label("varnorm - Test for normally distributed disturbances after var or svar")
BEGIN
  DEFINE _x _xsetbu
  DEFINE _y _top
  INCLUDE _bu_tsset

  GROUPBOX gb_statopt        _lft      _ss       _iwd      _ht2,	///
		label("Statistics to report")
  CHECKBOX ck_jbera          _indent   _ss       _cwd3_3   .,		///
		default(1)						///
		label("Jarque-Bera")					///
		option("jbera")
  CHECKBOX ck_skew           _iilft3_2 @         @         .,		///
		default(1)						///
		label("Skewness")					///
		option("skewness")
  CHECKBOX ck_kurt           _iilft3_3 @         @         .,		///
		default(1)						///
		label("Kurtosis")					///
		option("kurtosis")

  GROUPBOX  gb_est           _lft      _xls      _iwd      _ht6h,
  RADIO    rb_curres         _ilft     _ss       _cwd0     ., first	///
		label("Use active var or svar results")
  RADIO    rb_estimates      @         _ss       @         ., last	///
		label("Use alternative results:")			///
		onclickon(gaction main.cb_estimates.enable)		///
		onclickoff(gaction main.cb_estimates.disable)
  COMBOBOX cb_estimates      _indent2  _ss       _vnwd     .,		///
		dropdown						///
		option("estimates")					///
		contents(estimates)					///
		label("Alternative var or svar results")

  CHECKBOX ck_chol           _lft      _xxls     _cwd0     .,		///
		label("Use Cholesky decomposition")			///
		option("cholesky")
  SPINNER  sp_sep            @         _ls       _spwd 	   .,		///
		label("Separator every N lines")			///
		min(0)							///
		max(500)						///
		default(0)						///
		option("separator")
  TEXT     tx_sep            _spsep    @         _cwd1     .,		///
		label("Separator every N lines")
END

PROGRAM command
BEGIN
	if main.rb_estimates {
		require main.cb_estimates
	}

	put "varnorm"
	beginoptions
		option main.ck_jbera
		option main.ck_skew
		option main.ck_kurt
		optionarg main.cb_estimates
		option main.ck_chol
		optionarg /hidedefault main.sp_sep
	endoptions
END

/*
	predict (after xtgee)

*!  VERSION 2.0.2  25jan2011

*/

VERSION 11.0

INCLUDE _std_medium
DEFINE _dlght 310
INCLUDE header

HELP hlp1, view("help xtgee postestimation")
RESET res1

DIALOG main, label("predict - Prediction after estimation") tabtitle("Main")
BEGIN
  INCLUDE  _p_gentype

  GROUPBOX gb_opts           _lft      _ls      _iwd      _ht18h,	/*
		*/ label("Produce:")
  RADIO    rb_mu             _indent   _ss       _inwd     ., first	/*
		*/ option(mu)						/*
		*/ onclickon(script _p_gentype_score_off)		/*
	*/ label("Value of dependent variable (d*invlink(xb+offset))")
  RADIO    rb_rate           @         _ss       @         .,		/*
		*/ option(rate)						/*
		*/ onclickon(script _p_gentype_score_off)		/*
		*/ label("Value of dependent variable (invlink(xb))")
  RADIO    rb_pr_n           @         _ss       @         .,		/*
		*/ onclickon(script main_pr_n_on)			/*
		*/ onclickoff(script main_pr_n_off)			/*
		*/ label("Probability of y=n")
  RADIO    rb_pr_a_b       @         +45       @         .,		/*
		*/ onclickon(script main_pr_a_b_on)			/*
		*/ onclickoff(script main_pr_a_b_off)			/*
		*/ label("Probability of a<=y<=b")
  RADIO    rb_xb             @         +45      @         .,		/*
		*/ option(xb)						/*
		*/ onclickon(script _p_gentype_score_off)		/*
		*/ label("Linear prediction (xb)")
  RADIO    rb_stdp           @         _ss       @         .,		/*
		*/ option(stdp)						/*
		*/ onclickon(script _p_gentype_score_off)		/*
		*/ label("Standard error of the linear prediction")
  RADIO    rb_score          @         _ss       @         ., last	/*
		*/ option(score)					/*
		*/ onclickon(script _p_gentype_score_on)		/*
		*/ label("Equation-level scores")

  EDIT      ed_pr_n          _indent2  -110      _en7wd    .,           /*
                */ option(pr)                                           /*
                */ label("Probability of y=n")

DEFINE x @x
  TEXT      tx_pr_n          _en7sep   @         70        .,		/*
		*/ label("n")

  EDIT      ed_pr_a         x         +45       _en7wd    .,		/*
		*/ label("Probability of a<=y")
  TEXT      tx_pr_a         _en7sep   @         50        .,		/*
		*/ label("a")

  EDIT      ed_pr_b         +55       @        _en7wd     .,		/*
		*/ label("Probability of y<=b")
  TEXT      tx_pr_b         _en7sep   @        50         .,		/*
		*/ label("b")

  CHECKBOX ck_nooffset       _lft      +100     _iwd       .,		/*
		*/ option(nooffset)					/*
		*/ label("Ignore offset variable (if any)")

END

SCRIPT main_pr_n_on
BEGIN
	script _p_gentype_score_off
	main.ed_pr_n.enable
	main.tx_pr_n.enable
END

SCRIPT main_pr_n_off
BEGIN
	script _p_gentype_score_off
	main.ed_pr_n.disable
	main.tx_pr_n.disable
END

SCRIPT main_pr_a_b_on
BEGIN
	script _p_gentype_score_off
	main.ed_pr_a.enable
	main.tx_pr_a.enable
	main.ed_pr_b.enable
	main.tx_pr_b.enable
END

SCRIPT main_pr_a_b_off
BEGIN
	script _p_gentype_score_off
	main.ed_pr_a.disable
	main.tx_pr_a.disable
	main.ed_pr_b.disable
	main.tx_pr_b.disable
END

INCLUDE _p_gentype_sc
INCLUDE ifin
INCLUDE _type_list_fd

PROGRAM main_ed_pr_a_b_output
BEGIN
	put "pr("
	put main.ed_pr_a
	put ","
	put main.ed_pr_b
	put ")"
END

PROGRAM command
BEGIN
	put "predict "
	put " " /program _p_gentype_output
	INCLUDE _ifin_pr
	beginoptions
		option radio(main rb_mu rb_rate rb_xb rb_stdp rb_score)
		if main.rb_pr_n {
			require main.ed_pr_n
			optionarg main.ed_pr_n
		}
		if main.rb_pr_a_b {
			require main.ed_pr_a
			require main.ed_pr_b
			put " " /program main_ed_pr_a_b_output
		}
		option main.ck_nooffset
	endoptions
	INCLUDE _p_nooffset_pr
END
